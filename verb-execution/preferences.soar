##########################----------Preference rules wait-for-instructor------------------########################
### set preferences wait-for-instructor and others

sp {preference*wait-for-instructor*greater-than
   (state <s> ^operator <op1> +
   	      ^operator <op2> +) 
   (<op2> ^name wait-for-instructor)
   (<op1> ^category << primitive-action sub-primitive-action >>)
-->
   (<s> ^operator <op2> > <op1>)
}

sp {preference*wait-for-instructor*worse-than
   (state <s> ^operator <op1> +
   	      ^operator <op2> +) 
   (<op2> ^name wait-for-instructor)
   (<op1> ^category << object-processing svs smem communication natural-language interaction-stack >>)
-->
   (<s> ^operator <op2> < <op1>)
}

#########################-----------Preference rules category: object-processing----------------------#######################

### prefer interaction-stack operators over primitive-actions
sp {preference*object-processing*better-than
   (state <s> ^operator <op1> +
   	      ^operator <op2> +) 
   (<op2> ^category object-processing)
   (<op1> ^category << primitive-action sub-primitive-action new-environmental-action >>)
-->
   (<s> ^operator <op2> > <op1>)
}



### setting preferences of kind interpretation wrt object-processing and smem
sp {preference*interpretation*greater-than*2
   (state <s> ^operator <op1> +
   	      ^operator <op2> +) 
   (<op2> ^kind << interpretation message-interpretation >>)
   (<op1> ^category << object-processing smem svs >>)
-->
   (<s> ^operator <op2> > <op1>)
}

#########################-----------Preference rules category: svs ----------------------#######################

sp {preference*svs*better-than
   (state <s> ^operator <op1> +
   	      ^operator <op2> +) 
   (<op2> ^category svs)
   (<op1> ^category << primitive-action sub-primitive-action new-environmental-action object-processing >>)
-->
   (<s> ^operator <op2> > <op1>)
}


#########################-----------Preference rules category: smem ----------------------#######################

sp {preference*smem*better-than
   (state <s> ^operator <op1> +
   	      ^operator <op2> +) 
   (<op2> ^category smem)
   (<op1> ^category << primitive-action sub-primitive-action new-environmental-action object-processing svs >>)
-->
   (<s> ^operator <op2> > <op1>)
}

##########################----------Preference rules category: communication------------------########################

### setting preferences wrt primitive actions
sp {preference*communicate*greater-than
   (state <s> ^operator <op1> +
   	      ^operator <op2> +) 
   (<op2> ^category communication)
   (<op1> ^category << primitive-action sub-primitive-action new-environmental-action object-processing svs smem >>)
-->
   (<s> ^operator <op2> > <op1>)
}

### setting preferences of kind interpretation wrt primitive actions
sp {preference*interpretation*greater-than
   (state <s> ^operator <op1> +
   	      ^operator <op2> +) 
   (<op2> ^kind << interpretation message-interpretation >>)
   (<op1> ^category << primitive-action sub-primitive-action >>)
-->
   (<s> ^operator <op2> > <op1>)
}

##########################----------Preference rules category: nl------------------########################

### setting preferences wrt nl and primitive actions
sp {preference*natural-language*greater-than
   (state <s> ^operator <op1> +
   	      ^operator <op2> +) 
   (<op2> ^category natural-language)
   (<op1> ^category << primitive-action sub-primitive-action new-environmental-action object-processing svs smem communication >>)
-->
   (<s> ^operator <op2> > <op1>)
}

##########################----------Preference rules category: interaction-stack----------------------########################

### prefer interaction-stack operators over primitive-actions
sp {preference*interaction-stack*better-than
   (state <s> ^operator <op1> +
   	      ^operator <op2> +) 
   (<op2> ^category interaction-stack)
   (<op1> ^category << primitive-action sub-primitive-action new-environmental-action object-processing svs smem communication natural-language >>)
-->
   (<s> ^operator <op2> > <op1>)
}


### prefer pop-segment to push-segment
sp {preference*pop-segment*push-segment
   (state <s> ^operator <op1> +
   	      ^operator <op2> +)
   (<op1> ^name pop-segment)
   (<op2> ^name push-segment)
-->
   (<s> ^operator <op1> > <op2>)
}
   	 
