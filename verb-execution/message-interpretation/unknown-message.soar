# Pushed when the message coming in isn't interpreted

sp {propose*push-unknown-message
   (state <s> ^topstate <s>
              ^instructor-message <msg>)
   (<msg> -^processed)
-->
   (<s> ^operator <o> +)
   (<o> ^name push-unknown-message
        ^category communication)
}

sp {prefer*other*communication*over*push-unknown-message
   (state <s> ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name <> push-unknown-message
         ^category communication)
   (<o2> ^name push-unknown-message
         ^category communication)
-->
   (<s> ^operator <o1> > <o2>)
}

#sp {reject*if*expected*dialog-event
#   (state <s> ^interaction.stack.segment <segment>
#              ^instructor-message <m>
#              ^operator <o> +)
#   (<segment> ^purpose.satisfaction <sat>
#   	      ^originator agent)
#   (<sat> ^dialog-event <etype>)
#   (<m> ^type <etype>)
#   (<o> ^name push-unknown-message)
#-->
#   (<s> ^operator <o> -)
#}
   

sp {apply*push-unknown-message*mark-purpose
   (state <s> ^operator.name push-unknown-message
              ^instructor-message <msg>)
-->
   (<msg> ^purpose <p>)
   (<p> ^type unknown-message
        # AM: Changed satisfaction structure
        ^satisfaction.dialog-event.type unknown-message)
}

sp {propose*unknown-message
   (state <s> ^topstate <s>
              ^interaction.stack.segment.purpose <p>)
   (<p> ^type unknown-message)
-->
   (<s> ^operator <o> +)
   (<o> ^name unknown-message
        ^category communication)
}

sp {apply*unknown-message*send-message
   (state <s> ^operator.name unknown-message
              ^io.output-link <out>)
-->
   (<out> ^send-message <msg>)
   (<msg> ^type unknown-message
          ^fields <f>)
}

sp {apply*unknown-message*mark*performed
   (state <s> ^operator.name unknown-message
              ^status <status>)
-->
   #(<status> ^performed.dialog-event unknown-message)
   # AM: Changed performed structure
   (<status> ^performed.dialog-event <e>)
   (<e> ^type unknown-message)
}
