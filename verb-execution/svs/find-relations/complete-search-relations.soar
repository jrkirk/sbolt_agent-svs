sp {find-relations*propose*complete-relations-search*true
   (state <s> ^name find-relations
              ^obj <id>
			  ^relation <r>)
   (<s> ^relationships <rels>)
   (<r> ^0 true
        ^1 true
        ^matched true
        ^2 true)
-->
   (<s> ^operator <o> + =)
   (<o> ^name complete-relations-search
        ^category svs
        ^type true)
   (<rels> ^relations <r>)
}

#sp {find-relations*propose*complete-relations-search*true*all-aligned
#   (state <s> ^name find-relations
#              ^obj <id>
#              ^predicate <p1>
#			  ^relation <r>)
#   (<p1> ^all-aligned true)
#   (<s> ^relationships <rels>)
#   (<r> ^0 true ^0 x-aligned
#        ^1 true ^1 y-aligned
#        ^2 true ^2 z-aligned)
#-->
#   (<s> ^operator <o> + =)
#   (<o> ^name complete-relations-search
#        ^category svs
#        ^type true)
#   (<rels> ^relations <r>)
#}

sp {find-relations*propose*found-possible-match
   (state <s> ^name find-relations
              ^obj <id>
              ^predicate <p1>
			  ^relation <r>)
   (<s> ^relationships <rels>)
   (<p1> ^all-aligned true)
   (<r> ^0 true
        ^1 true
        ^2 true
       -^matched)
-->
   (<s> ^operator <o> + =)
   (<o> ^name found-possible-match
        ^category svs
        ^relation <r>)
}

sp {find-relations*propose*found-possible-match*not-all-aligned
   (state <s> ^name find-relations
              ^obj <id>
              ^predicate <p1>
			  ^relation <r>)
   (<s> ^relationships <rels>)
   (<p1> ^all-aligned false)
   (<r> ^0 true
        ^1 true
        ^2 true
       -^matched)
  -{(<r> ^0 x-aligned ^1 y-aligned ^2 z-aligned)}
-->
   (<s> ^operator <o> + =)
   (<o> ^name found-possible-match
        ^category svs
        ^relation <r>)
}

sp {find-relations*apply*found-possible-match*extract_once-distance
   (state <s> ^name find-relations
              ^operator <o>
              ^topstate <ss>
              ^obj <id>
              ^predicate <p1>
              ^relationships <rels>)
   (<o> ^name found-possible-match
        ^relation <r>)
   (<r> ^obj <objb> ^name <type>)
   (<p1> ^name <type>
         ^relation.axis <axis>)
   (<ss> ^svs.command <cmd>)
   (<ss> ^svs.spatial-scene <sc>)
   (<sc> ^child <C> ^child <C2>)
   (<C> ^id <obja1> ^id-int <id>)
   (<C2> ^id <objb1> ^id-int <objb>)
-->
   (<cmd> ^extract_once <P11>)
   (<P11> ^a <N7> ^b <N8> ^type distance_xyz ^axis <axis>)
   (<N7> ^type node ^id <obja1>)
   (<N8> ^type node ^id <objb1>)
}

sp {find-relations*apply*found-possible-match*matched-true
   (state <s> ^name find-relations
              ^operator <o>
              ^topstate <ss>
              ^obj <id>
              ^predicate <p1>
              ^relationships <rels>)
   (<o> ^name found-possible-match
        ^relation <r>)
   (<r> ^obj <objb> ^name <type>)
   (<s> ^<objb> <obj0>
        ^<objb> <obj1>
        ^<objb> <obj2>)
   (<obj0> ^0 true)
   (<obj1> ^1 true)
   (<obj2> ^2 true)
-->
   (<r> ^matched true)
}

sp {find-relations*apply*found-possible-match*matched-false
   (state <s> ^name find-relations
              ^operator <o>
              ^topstate <ss>
              ^obj <id>
              ^predicate <p1>
              ^relationships <rels>)
   (<o> ^name found-possible-match
        ^relation <r>)
   (<r> ^obj <objb> ^name <type>)
   (<s> ^<objb>.<< 0 1 2 >> false)
-->
   (<r> ^matched false)
} 

sp {find-relations*apply*found-possible-match*check-distance
   (state <s> ^name find-relations
              ^operator <o>
              ^topstate <ss>
              ^obj <id>
              ^predicate <p1>
              ^relationships <rels>)
   (<o> ^name found-possible-match
        ^relation <r>)
   (<r> ^obj <objb> ^name <type> -^<axis> << x-aligned y-aligned z-aligned >>)
   (<p1> ^name <type>
         ^relation <rel>)
   (<rel> ^axis <axis> ^avg <avg> ^top <top> ^bottom <bot> -^count 1)
   (<ss> ^svs.command <cmd>)
   (<cmd> ^extract_once <ED1>)
   (<ED1> ^type distance_xyz ^axis <axis> ^result.record <rec>)
   (<rec> ^value { >= <bot> <= <top> } ^params <A>)
   (<A> ^a <obja1>
        ^b <objb1>)
   (<ss> ^svs.spatial-scene <sc>)
   (<sc> ^child <C> ^child <C2>)
   (<C> ^id <obja1> ^id-int <id>)
   (<C2> ^id <objb1> ^id-int <objb>)
-->
   (<s> ^<objb>.<axis> true)
}

sp {find-relations*apply*found-possible-match*check-distance*false
   (state <s> ^name find-relations
              ^operator <o>
              ^topstate <ss>
              ^obj <id>
              ^predicate <p1>
              ^relationships <rels>)
   (<o> ^name found-possible-match
        ^relation <r>)
   (<r> ^obj <objb> ^name <type> -^<axis> << x-aligned y-aligned z-aligned >>)
   (<p1> ^name <type>
         ^relation <rel>)
   (<rel> ^axis <axis> ^avg <avg> ^top <top> ^bottom <bot> -^count 1)
   (<ss> ^svs.command <cmd>)
   (<cmd> ^extract_once <ED1>)
   (<ED1> ^type distance_xyz ^axis <axis> ^result.record <rec>)
   (<rec> -^value { >= <bot> <= <top> } ^params <A>)
   (<A> ^a <obja1>
        ^b <objb1>)
   (<ss> ^svs.spatial-scene <sc>)
   (<sc> ^child <C> ^child <C2>)
   (<C> ^id <obja1> ^id-int <id>)
   (<C2> ^id <objb1> ^id-int <objb>)
-->
   (<s> ^<objb>.<axis> false)
}

sp {find-relations*apply*found-possible-match*check-distance*true-for-one
   (state <s> ^name find-relations
              ^operator <o>
              ^topstate <ss>
              ^obj <id>
              ^predicate <p1>
              ^relationships <rels>)
   (<o> ^name found-possible-match
        ^relation <r>)
   (<r> ^obj <objb> ^name <type> -^<axis> << x-aligned y-aligned z-aligned >>)
   (<p1> ^name <type>
         ^relation <rel>)
   (<rel> ^axis <axis> ^avg <avg> ^top <top> ^bottom <bot> ^count 1)
   (<ss> ^svs.command <cmd>)
   (<cmd> ^extract_once <ED1>)
   (<ED1> ^type distance_xyz ^axis <axis> ^result.record <rec>)
   (<rec> ^value <val> ^params <A>)
   (<A> ^a <obja1>
        ^b <objb1>)
   (<ss> ^svs.spatial-scene <sc>)
   (<sc> ^child <C> ^child <C2>)
   (<C> ^id <obja1> ^id-int <id>)
   (<C2> ^id <objb1> ^id-int <objb>)
-->
   (<s> ^<objb>.<axis> true)
}

sp {find-relations*apply*found-possible-match*check-distance*true-aligned
   (state <s> ^name find-relations
              ^operator <o>
              ^topstate <ss>
              ^obj <id>
              ^predicate <p1>
              ^relationships <rels>)
   (<o> ^name found-possible-match
        ^relation <r>)
   (<r> ^obj <objb> ^name <type> ^<axis> << x-aligned y-aligned z-aligned >>)
-->
   (<s> ^<objb>.<axis> true)
}

#sp {find-relations*complete-relations-search*false
#   (state <s> ^name find-relations
#              ^obj <id>
#             #-^superstate.relationships
#			  ^relation <r>)
#   (<r> ^result false)
#-->
#   (<s> ^operator <o> + =)
#   (<o> ^name complete-relations-search
#        ^type false
#        ^category svs)
#}


sp {find-relations*propose*complete-relations-search*nonefound
   (state <s> ^name find-relations
              ^obj <id>
              ^relationships <rels>)
   (<rels> -^relations
           -^failure prep-not-found)
-->
   (<rels> ^failure none-found)
   (<s> ^operator <o> + =)
   (<o> ^name complete-relations-search
        ^type none
        ^category svs)
}

sp {find-relations*propose*complete-relations-search*prep-not-found
   (state <s> ^name find-relations
              ^obj <id>
              ^relationships <rels>)
   (<rels> ^failure prep-not-found)
-->
   (<s> ^operator <o> + =)
   (<o> ^name complete-relations-search
        ^type none
        ^category svs)
}


sp {find-relations*apply*relations-match
   (state <s> ^name find-relations
              ^operator <o>
              ^superstate <ss>
              ^relationships <rels>)
   (<o> ^name complete-relations-search)
-->
   (<ss> ^relationships <rels>)
}
