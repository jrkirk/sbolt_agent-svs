# teaching-request
# 	If use-interaction=true and no matches were found for the preposition, 
#   ask the user for more information
# The teaching interaction is finished with the single word 'cancel' or 'finished'

sp {find-projection*propose*teaching-request
   (state <s> ^name find-projection
              ^stat default
              ^prep <prep>
              ^interaction true
             -^topstate.interaction.stack.segment.info.status.no-teaching true)
-->
   (<s> ^operator <o> + =)
   (<o> ^name teaching-request
        ^category svs
        ^prep <prep>)
}

# Creates a teaching-request query 
sp {find-projection*apply*teaching-request*create*query
   (state <s> ^name find-projection
              ^operator <o>
              ^topstate.interaction.stack.segment <seg>)
   (<seg> ^info <i>)
   (<o> ^name teaching-request
        ^prep <prep>)
-->
   (<s> ^query <q>)
   (<i> ^status <stat>)
   (<q> ^type teaching-request
        ^segment <seg>
        ^status <stat>
        ^description <desc>
        ^prep <prep>)
   (<desc> ^determiner preposition)
}

# Copies the name of the preposition to the description
sp {find-projection*apply*teaching-request*copy*name
   (state <s> ^name find-projection
              ^operator <o>
              ^query.description <desc>)
   (<o> ^name teaching-request
        ^prep <prep>)
-->
   (<desc> ^word <prep>)
}

# Copies the query to the topstate
sp {find-projection*apply*teaching-request*copy*query
   (state <s> ^name find-projection
              ^operator.name teaching-request
              ^topstate.queries <qs>
              ^query <q>)
   (<q> ^description.word)
-->
   (<qs> ^query <q>)
}


