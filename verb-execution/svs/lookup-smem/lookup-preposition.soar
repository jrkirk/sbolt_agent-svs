sp {lookup-smem*preposition*query
   (state <s> ^name lookup-preposition
              ^superstate.operator.type <type>
              ^smem.command <sc>)
-->
   (<sc> ^query <q>)
   (<q> ^name <type>)
}


sp {lookup-smem*preposition*query*failure
   (state <s> ^name lookup-preposition
              ^superstate <ss>
              ^smem.result.failure <cue>)
-->
   (<ss> ^status default)
}

sp {lookup-smem*preposition*query*success
   (state <s> ^name lookup-preposition
              ^superstate.operator.type <type>
              ^smem.result.success <cue>
              ^smem.result <res>)
   (<res> ^retrieved <ret>)
-->
   (<s> ^predicate <ret>
        ^status old)
}

sp {lookup-smem*preposition*propose*lookup-relations
   (state <s> ^name lookup-preposition
              ^superstate.operator.type <type>
              ^predicate <p1>
              ^status old
             -^relation <rel>)
   (<p1> ^relation <rel>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name lookup-relations
        ^category svs
        ^lte <rel>)
}


sp {lookup-smem*preposition*propose*complete-lookup-relations
   (state <s> ^name lookup-preposition
              ^superstate.operator.type <type>
              ^predicate <p1>
              ^status old)
-->
   (<s> ^operator <o> +)
   (<o> ^name complete-lookup-relations
        ^category svs)
}
sp {lookup-smem*preposition*copy*relations
   (state <s> ^name lookup-preposition
              ^superstate <ss>
              ^relation <rel>
              ^status old)
   (<s> ^operator <o>)
   (<o> ^name complete-lookup-relations)
-->
   (<ss> ^relation <rel>)
}

sp {lookup-smem*preposition*apply*complete-lookup-relations
   (state <s> ^name lookup-preposition
              ^superstate <ss>
              ^predicate <p1>
              ^status old)
   (<s> ^operator <o>)
   (<o> ^name complete-lookup-relations)
-->
   (<ss> ^predicate <p1>
         ^status old)
}