

sp {relation-messaging*propose*index-object1
   (state <s> ^topstate <s>
              ^interaction.stack.segment <seg>)
   (<seg> ^purpose <p>
         -^info.obj1)
   (<p> ^type relation-message
        ^parameters.object1 <obj1>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name index-object
        ^use-interaction true
		^allow-ties true
  		^infer-match true # AM: Allows index-object to guess a match if there is only one partial match
        ^obj 1
        ^category object-processing
	    ^object <obj1>
	    ^context-id object1)
}

sp {relation-messaging*propose*index-object2
   (state <s> ^topstate <s>
              ^interaction.stack.segment <seg>)
   (<seg> ^purpose <p>
         -^info.obj2
          ^info.obj1)
   (<p> ^type relation-message
        ^parameters.object2 <obj2>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name index-object
        ^use-interaction true
		^allow-ties true
  		^infer-match true # AM: Allows index-object to guess a match if there is only one partial match
        ^obj 2
        ^category object-processing
	    ^object <obj2>
	    ^context-id object2)
}



sp {relation-messaging*apply*index-object*results1
   (state <s> ^operator <o>
              ^interaction.stack.segment <seg>)
   (<seg> ^purpose.type relation-message
          ^info <i>)
   (<o> ^name index-object
        ^obj 1
        ^match.id <id>)
-->
   (<i> ^obj1 <id>)
}

sp {relation-messaging*apply*index-object*results2
   (state <s> ^operator <o>
              ^interaction.stack.segment <seg>)
   (<seg> ^purpose.type relation-message
          ^info <i>)
   (<o> ^name index-object
        ^obj 2
        ^match.id <id>)
-->
   (<i> ^obj2 <id>)
}

sp {relation-messaging*apply*index-object*failure
   (state <s> ^operator <o>
              ^interaction.stack.segment.purpose.type relation-message
              ^io.output-link <out>
              ^topstate.status <status>)
   (<o> ^name index-object
        ^obj << 1 2 >>
        ^match failure)              
-->
   (<out> ^send-message <msg>)
   (<msg> ^type no-object
          ^fields.object failure)
   (<status> ^performed.learning-event.type preposition)
}



sp {relation-messaging*propose*process-svs-message
   (state <s> ^interaction.stack.segment <seg>)
   (<seg> ^purpose <p>
          ^info.obj1 <id1>
          ^info.obj2 <id2>)
   (<p> ^type relation-message
        ^parameters.prep <prep>)
-->
   (<s> ^operator <o> +)
   (<o> ^name process-svs-message
        ^category svs
	    ^message <m>)
   (<m> ^fields <f> ^type svs-label)
   (<f> ^objects <object>)
   (<object> ^obj <obja>
             ^obj <objb>
             ^type <prep>)
   (<obja> ^id <id1> ^index 1)
   (<objb> ^id <id2> ^index 2)
} 


