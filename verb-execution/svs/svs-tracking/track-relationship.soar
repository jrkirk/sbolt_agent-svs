sp {svs-tracking*propose*lookup-preposition
   (state <s> ^name svs-tracking
             -^status
              ^prep <prep>)
-->
   (<s> ^operator <o> +)
   (<o> ^name lookup-preposition
        ^category svs
        ^type <prep>)
}

sp {svs-tracking*preposition-not-found
   (state <s> ^name svs-tracking
              ^superstate <ss>
              ^status default
              ^result <res>)
-->
   (<res> ^failure prep-not-found)
}


sp {svs-tracking*extract-filters
   (state <s> ^name svs-tracking
              ^a <obja>
              ^b <objb>
              ^prep <prep>
              ^predicate <p>
              ^topstate.svs.spatial-scene <sc>
              ^topstate.svs.command <cmd>)
   (<p> ^name <prep>
        ^relation <rel>)
   (<rel> ^type <type>)
   (<sc> ^child <C> ^child <C2>)
   (<C> ^id <obja1> ^id-int <obja>)
   (<C2> ^id <objb1> ^id-int <objb>)
-->
   (<cmd> ^extract <E11>)
   (<E11> ^a <N7> ^b <N8> ^type <type>)
   (<N7> ^type node ^name <obja1>)
   (<N8> ^type node ^name <objb1>)
}

sp {svs-tracking*test-every-relations*negative
   (state <s> ^name svs-tracking
              ^a <obja>
              ^b <objb>
              ^prep <prep>
              ^predicate.relation.type <type>
              ^tracked-relation <tr>
              ^topstate.svs.spatial-scene <sc>
              ^topstate.svs.command <cmd>)
   (<cmd> ^extract <E11>)
   (<E11> ^type <type> ^result.negative.atom <A1>)
   (<sc> ^child <C> ^child <C2>)
   (<C> ^id <obja1> ^id-int <obja>)
   (<C2> ^id <objb1> ^id-int <objb>)
   (<A1> ^a <obja1> 
         ^b <objb1>)
-->
   (<tr> ^relation <rel>)
   (<rel> ^result false ^type <type>)
}

sp {svs-tracking*test-every-relations*positive
   (state <s> ^name svs-tracking
              ^a <obja>
              ^b <objb>
              ^prep <prep>
              ^predicate.relation.type <type>
              ^tracked-relation <tr>
              ^topstate.svs.spatial-scene <sc>
              ^topstate.svs.command <cmd>)
   (<cmd> ^extract <E11>)
   (<E11> ^type <type> ^result.positive.atom <A1>)
   (<sc> ^child <C> ^child <C2>)
   (<C> ^id <obja1> ^id-int <obja>)
   (<C2> ^id <objb1> ^id-int <objb>)
   (<A1> ^a <obja1> 
         ^b <objb1>)
-->
   (<tr> ^relation <rel>)
   (<rel> ^result true ^type <type>)
}





   