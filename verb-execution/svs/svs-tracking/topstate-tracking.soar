sp {svs-tracking*propose*lookup-preposition
   (state <s> ^topstate <s>
              ^track-relation <relation>)
   (<relation> ^object1.id <ida>
               ^object2.id <idb>
              -^found-prep
               ^prep <prep>)
-->
   (<s> ^operator <o> +)
   (<o> ^name lookup-preposition
        ^category svs
        ^type <prep>)
}

sp {svs-tracking*preposition*store*found-prep
   (state <s> ^topstate <s>
              ^found-prep <fprep>
              ^track-relation <relation>)
  (<fprep> ^type <prep>)
  (<relation> ^prep <prep>
             -^found-prep <fprep1> <> <fprep>)
-->
   (<relation> ^found-prep <fprep>)
}

sp {svs-tracking*preposition*not-found
   (state <s> ^topstate <s>
              ^track-relation <relation>)
  (<fprep> ^type <prep> ^stat default)
  (<relation> ^prep <prep> ^found-prep <fprep>)
-->
   (<relation> ^failure prep-not-found)
}
   

sp {svs-tracking*extract-filters
   (state <s> ^topstate <s>
              ^track-relation <relation>
              ^topstate.svs.spatial-scene <sc>
              ^topstate.svs.command <cmd>)
   (<relation> ^object1.id <ida>
               ^object2.id <idb>
               ^prep <prep>
               ^found-prep <fprep>)
   (<fprep> ^type <prep> ^stat old ^predicate <p>)
   (<p> ^name <prep>
        ^relation <rel>)
   (<rel> ^type <type>)
   (<sc> ^child <C> ^child <C2>)
   (<C> ^id <obja1> ^id-int <ida>)
   (<C2> ^id <objb1> ^id-int <idb>)
-->  
   (<cmd> ^extract <E11>)
   (<relation> ^tracking-extract <E11>)
   (<E11> ^a <N7> ^b <N8> ^type <type>)
   (<N7> ^type node ^name <obja1>)
   (<N8> ^type node ^name <objb1>)
}


sp {svs-tracking*test-relations*negative
   (state <s> ^topstate <s>
              ^track-relation <relation>         
              ^topstate.svs.spatial-scene <sc>
              ^topstate.svs.command <cmd>)
   (<relation> ^object1.id <obja>
               ^object2.id <objb>
               ^tracking-extract <E11>
               ^prep <prep>
               ^tracked-relation <tr>
               ^found-prep <fprep>)
   (<fprep> ^type <prep> ^stat old ^predicate <p>)
   (<p> ^relation.type <type>)
   (<cmd> ^extract <E11>)
   (<E11> ^type <type> ^result.negative.atom <A1>)
   (<sc> ^child <C> ^child <C2>)
   (<C> ^id <obja1> ^id-int <obja>)
   (<C2> ^id <objb1> ^id-int <objb>)
   (<A1> ^a <obja1> 
         ^b <objb1>)
-->
   (<tr> ^relation <rel>)
   (<rel> ^result false ^type <type>)
}

sp {svs-tracking*test-relations*positive
   (state <s> ^topstate <s>
              ^track-relation <relation>
              ^topstate.svs.spatial-scene <sc>
              ^topstate.svs.command <cmd>)
   (<fprep> ^type <prep> ^stat old ^predicate <p>)
   (<p> ^relation.type <type>)
   (<relation> ^object1.id <obja>
               ^object2.id <objb>
               ^tracking-extract <E11>
               ^prep <prep>
               ^tracked-relation <tr>
               ^found-prep <fprep>)
   (<cmd> ^extract <E11>)
   (<E11> ^type <type> ^result.positive.atom <A1>)
   (<sc> ^child <C> ^child <C2>)
   (<C> ^id <obja1> ^id-int <obja>)
   (<C2> ^id <objb1> ^id-int <objb>)
   (<A1> ^a <obja1> 
         ^b <objb1>)
-->
   (<tr> ^relation <rel>)
   (<rel> ^result true ^type <type>)
}



sp {svs-tracking*propose*complete-svs-tracking
   (state <s> ^topstate <s>
              ^track-relation <relation>)
   (<relation> ^object1.id <obja>
               ^object2.id <objb>
              -^failure prep-not-found
              -^tracked-relation.relation.result false
               ^tracked-relation.relation.result true
               ^prep <prep>
               ^found-prep <fprep>)
   (<fprep> ^type <prep>)
-->
   (<relation> ^complete success)
}