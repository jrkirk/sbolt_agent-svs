#sp {svs-tracking*propose*lookup-preposition
#   (state <s> ^topstate <s>
#              ^track-relation <relation>)
#   (<relation> ^object1.id <ida>
#               ^object2.id <idb>
#              -^found-prep
#               ^prep <prep>)
#-->
#   (<s> ^operator <o> +)
#   (<o> ^name lookup-preposition
#        ^category svs
#        ^type <prep>)
#}
#
#sp {svs-tracking*preposition*store*found-prep
#   (state <s> ^topstate <s>
#              ^found-prep <fprep>
#              ^track-relation <relation>)
#  (<fprep> ^type <prep>)
#  (<relation> ^prep <prep>
#             -^found-prep <fprep1> <> <fprep>)
#-->
#   (<relation> ^found-prep <fprep>)
#}

sp {svs-tracking*preposition*not-found
   (state <s> ^topstate <s>
              ^track-relation <relation>)
  (<fprep> ^type <prep> ^stat default)
  (<relation> ^prep <prep> ^found-prep <fprep>)
-->
   (<relation> ^failure prep-not-found)
}
   

sp {svs-tracking*extract-filters
   (state <s> ^topstate <s>
              ^track-relation <relation>
              ^topstate.svs.spatial-scene <sc>
              ^topstate.svs.command <cmd>)
   (<relation> ^object1.id <ida>
               ^object2.id <idb>
               ^prep <prep>
               ^found-prep <fprep>)
   (<fprep> ^type <prep> ^stat old ^predicate <p>)
   (<p> ^name <prep>
        ^relation <rel>)
   (<rel> ^type <type>)
   (<sc> ^child <C> ^child <C2>)
   (<C> ^id <obja1> ^id-int <ida>)
   (<C2> ^id <objb1> ^id-int <idb>)
-->  
   (<cmd> ^extract <E11>)
   (<relation> ^tracking-extract <E11>)
   (<E11> ^a <N7> ^b <N8> ^type <type>)
   (<N7> ^type node ^id <obja1>)
   (<N8> ^type node ^id <objb1>)
}

sp {svs-tracking*extract-filters*distance
   (state <s> ^topstate <s>
              ^track-relation <relation>
              ^topstate.svs.spatial-scene <sc>
              ^topstate.svs.command <cmd>)
   (<relation> ^object1.id <ida>
               ^object2.id <idb>
               ^prep <prep>
               ^found-prep <fprep>)
   (<fprep> ^type <prep> ^stat old ^predicate <p>)
   (<p> ^name <prep>
        ^relation <rel>)
   (<rel> ^axis <axis>)
   (<sc> ^child <C> ^child <C2>)
   (<C> ^id <obja1> ^id-int <ida>)
   (<C2> ^id <objb1> ^id-int <idb>)
-->  
   (<cmd> ^extract <E11>)
   (<relation> ^tracking-extract <E11>)
   (<E11> ^a <N7> ^b <N8> ^type distance_xyz ^axis <axis>)
   (<N7> ^type node ^id <obja1>)
   (<N8> ^type node ^id <objb1>)
}


#sp {svs-tracking*test-relations*negative
#   (state <s> ^topstate <s>
#              ^track-relation <relation>         
#              ^topstate.svs.spatial-scene <sc>
#              ^topstate.svs.command <cmd>)
#   (<relation> ^object1.id <obja>
#               ^object2.id <objb>
#               ^tracking-extract <E11>
#               ^prep <prep>
#               ^tracked-relation <tr>
#               ^found-prep <fprep>)
#   (<fprep> ^type <prep> ^stat old ^predicate <p>)
#   (<p> ^relation.type <type>)
#   (<cmd> ^extract <E11>)
#   (<E11> ^type <type> ^result.negative.atom <A1>)
#   (<sc> ^child <C> ^child <C2>)
#   (<C> ^id <obja1> ^id-int <obja>)
#   (<C2> ^id <objb1> ^id-int <objb>)
#   (<A1> ^a <obja1> 
#         ^b <objb1>)
#-->
#   (<tr> ^relation <rel>)
#   (<rel> ^result false ^type <type>)
#}

sp {svs-tracking*test-relations*positive*aligned
   (state <s> ^topstate <s>
              ^track-relation <relation>
              ^svs.spatial-scene <sc>
              ^svs.command <cmd>)
   (<fprep> ^type <prep> ^stat old ^predicate <p>)
   (<p> ^relation <r>)
   (<r> ^type {<type> << x-aligned y-aligned z-aligned >>}
        ^axis <axis>)
   (<relation> ^object1.id <obja>
               ^object2.id <objb>
               ^tracking-extract <E11>
               ^prep <prep>
               ^tracked-relation <tr>
               ^found-prep <fprep>)
   (<cmd> ^extract <E11>)
   (<E11> ^type <type> ^result.record <rec>)
   (<rec> ^value t ^params <A1>)
   (<sc> ^child <C> ^child <C2>)
   (<C> ^id <obja1> ^id-int <obja>)
   (<C2> ^id <objb1> ^id-int <objb>)
   (<A1> ^a <obja1> 
         ^b <objb1>)
-->
   (<tr> ^relation <rel>)
   (<rel> ^<axis> true ^<axis> <type>)
}

sp {svs-tracking*test-relations*positive
   (state <s> ^topstate <s>
              ^track-relation <relation>
              ^topstate.svs.spatial-scene <sc>
              ^topstate.svs.command <cmd>)
   (<fprep> ^type <prep> ^stat old ^predicate <p>)
   (<p> ^relation <r>)
   (<r> ^type {<t> << x-greater-than y-greater-than z-greater-than x-less-than y-less-than z-less-than >>}
        ^axis <axis>
        ^avg <avg> ^top <top> ^bottom <bot> -^count 1)
   (<relation> ^object1.id <obja>
               ^object2.id <objb>
               ^tracking-extract <E11>
               ^tracking-extract <ED1>
               ^prep <prep>
               ^tracked-relation <tr>
               ^found-prep <fprep>)
   (<cmd> ^extract <ED1>)
   (<ED1> ^type distance_xyz ^axis <axis> ^result.record <recd>)
   (<recd> ^value { >= <bot> <= <top> } ^params <A>)
   (<A> ^a <obja1> 
        ^b <objb1>)
   (<cmd> ^extract <E11>)
   (<E11> ^type <t> ^result.record <rec>)
   (<rec> ^value t ^params <A1>)
   (<sc> ^child <C> ^child <C2>)
   (<C> ^id <obja1> ^id-int <obja>)
   (<C2> ^id <objb1> ^id-int <objb>)
   (<A1> ^a <obja1> 
         ^b <objb1>)
-->
   (<tr> ^relation <rel>)
   (<rel> ^<axis> true ^<axis> <t>)
}

sp {svs-tracking*test-relations*positive*true-for-one
   (state <s> ^topstate <s>
              ^track-relation <relation>
              ^topstate.svs.spatial-scene <sc>
              ^topstate.svs.command <cmd>)
   (<fprep> ^type <prep> ^stat old ^predicate <p>)
   (<p> ^relation <r>)
   (<r> ^type {<t> << x-greater-than y-greater-than z-greater-than x-less-than y-less-than z-less-than >>}
        ^axis <axis>
        ^avg <avg> ^top <top> ^bottom <bot> ^count 1)
   (<relation> ^object1.id <obja>
               ^object2.id <objb>
               ^tracking-extract <E11>
               ^prep <prep>
               ^tracked-relation <tr>
               ^found-prep <fprep>)
   (<cmd> ^extract <E11>)
   (<E11> ^type <t> ^result.record <rec>)
   (<rec> ^value t ^params <A1>)
   (<sc> ^child <C> ^child <C2>)
   (<C> ^id <obja1> ^id-int <obja>)
   (<C2> ^id <objb1> ^id-int <objb>)
   (<A1> ^a <obja1> 
         ^b <objb1>)
-->
   (<tr> ^relation <rel>)
   (<rel> ^<axis> true ^<axis> <t>)
}



sp {svs-tracking*propose*complete-svs-tracking
   (state <s> ^topstate <s>
              ^track-relation <relation>)
   (<relation> ^object1.id <obja>
               ^object2.id <objb>
              -^failure prep-not-found
#              -^tracked-relation.relation.result false
               ^tracked-relation <tr>
               ^prep <prep>
               ^found-prep <fprep>)
   (<tr> ^relation <rel1> ^relation <rel2> ^relation <rel3>)
   (<rel1> ^0 true)
   (<rel2> ^1 true)
   (<rel3> ^2 true)   
   (<fprep> ^type <prep> ^predicate <p>)
   (<p> ^all-aligned true)
-->
   (<relation> ^complete success)
}

sp {svs-tracking*propose*complete-svs-tracking*not-aligned
   (state <s> ^topstate <s>
              ^track-relation <relation>)
   (<relation> ^object1.id <obja>
               ^object2.id <objb>
              -^failure prep-not-found
#              -^tracked-relation.relation.result false
               ^tracked-relation <tr>
               ^prep <prep>
               ^found-prep <fprep>)
   (<tr> ^relation <rel1> ^relation <rel2> ^relation <rel3>)
   (<rel1> ^0 true)
   (<rel2> ^1 true)
   (<rel3> ^2 true)   
   (<fprep> ^type <prep> ^predicate <p>)
   (<p> ^all-aligned false)
  -{(<rel1> ^0 x-aligned)
    (<rel2> ^1 y-aligned)
    (<rel3> ^2 z-aligned)}
-->
   (<relation> ^complete success)
}