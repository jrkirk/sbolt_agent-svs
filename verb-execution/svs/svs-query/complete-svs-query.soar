#find ids for both objects then process in svs
sp {svs-query*propose*find-relations
   (state <s> ^name svs-query
              -^relationships
              ^superstate.operator <so>)
   (<so> ^message.fields.objects <objs>)
   (<objs> ^obj <object>
           ^type <prep>)
   (<object> ^id <id> ^index <ind>)
-->
   (<s> ^operator <o>)
   (<o> ^name find-relations
        ^category svs
        ^obj <id>
        ^ind <ind>
        ^prep <prep>) 
}

sp {svs-query*apply*find-relations
   (state <s> ^name svs-query
              ^operator <o>)
   (<o> ^name find-relations)
-->
   (<s> ^operator <o> + =)
}

#sp {svs-query*elaborate*find-relations*copy-relationships
#   (state <s> ^name svs-query
#              ^operator <o>)
#   (<o> ^name find-relations
#        ^relationships <rels>)
#-->
#   (<s> ^relationships <rels>)
#}

sp {svs-query*propose*describe-object
   (state <s> ^name svs-query
               ^relationships.relations.obj <obj-id>
              -^described <obj>
               ^topstate.object <obj>)
   (<obj> ^id <obj-id>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name describe-object
        ^category svs
        ^object <obj>)
}

sp {svs-query*apply*describe-object*send-message
   (state <s> ^name svs-query
              ^relationships <r>
              ^operator <o>
              ^topstate.io.output-link <out>)
   (<o> ^name describe-object
        ^object <obj>
        ^description <desc>)
-->
   (<s> ^sent-message.<msg> <obj>)
   (<out> ^send-message <msg>)
   (<msg> ^type describe-object
          ^fields.object <desc>)
}

sp {svs-query*apply*describe-object*mark-described
   (state <s> ^name svs-query
              ^sent-message.<msg> <obj>
              ^relationships <r>
              ^operator <o>)
   (<o> ^name describe-object
        ^object <obj>
        ^description <desc>)
   (<msg> ^status complete)
-->
   (<s> ^described <obj>)
}
   
   
sp {svs-query*propose*complete-svs-query
   (state <s> ^name svs-query
              ^relationships <r>)
-->
   (<s> ^operator <o> +)
   (<o> ^name complete-svs-query
        ^category svs)
}


sp {svs-query*apply*complete-svs-query
   (state <s> ^name svs-query
              ^relationships <r>
              ^operator.name complete-svs-query
              ^topstate.status <status>)
-->
   (<status> ^performed.action-event describe)
}

sp {svs-query*apply*complete-svs-query*found-none
   (state <s> ^name svs-query
              ^relationships <r>
             -^described
              ^operator.name complete-svs-query
              ^io.output-link <out>)
-->
   (<out> ^send-message <msg>)
   (<msg> ^type describe-object
          ^fields.object failure)
}



