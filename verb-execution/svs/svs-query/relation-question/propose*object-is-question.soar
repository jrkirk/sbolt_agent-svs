

sp {object-is-question-rel*propose*index-object1
   (state <s> ^topstate <s>
              ^interaction.stack.segment <seg>)
   (<seg> ^purpose <p>
         -^info.obj1)
   (<p> ^type object-is-question-rel
        ^parameters.object1 <obj1>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name index-object
        ^use-interaction true
		^allow-ties true
        ^infer-match true
        ^obj 1
        ^context-id 1
        ^category object-processing
	    ^object <obj1>)
}

sp {object-is-question-rel*propose*index-object2
   (state <s> ^topstate <s>
              ^interaction.stack.segment <seg>)
   (<seg> ^purpose <p>
         -^info.obj2
          ^info.obj1)
   (<p> ^type object-is-question-rel
        ^parameters.object2 <obj2>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name index-object
        ^use-interaction true
		^allow-ties true
        ^infer-match true
        ^obj 2
        ^context-id 2
        ^category object-processing
	    ^object <obj2>)
}



sp {object-is-question-rel*apply*index-object*results1
   (state <s> ^operator <o>
              ^interaction.stack.segment <seg>)
   (<seg> ^purpose.type object-is-question-rel
          ^info <i>)
   (<o> ^name index-object
        ^obj 1
        ^match.id <id>)
-->
   (<i> ^obj1 <id>)
}

sp {object-is-question-rel*apply*index-object*results2
   (state <s> ^operator <o>
              ^interaction.stack.segment <seg>)
   (<seg> ^purpose.type object-is-question-rel
          ^info <i>)
   (<o> ^name index-object
        ^obj 2
        ^match.id <id>)
-->
   (<i> ^obj2 <id>)
}

sp {object-is-question-rel*apply*index-object*failure
   (state <s> ^operator <o>
              ^interaction.stack.segment.purpose.type object-is-question-rel
              ^io.output-link <out>
              ^topstate.status <status>)
   (<o> ^name index-object
        ^obj << 1 2 >>
        ^match failure)              
-->
   (<out> ^send-message <msg>)
   (<msg> ^type no-object
          ^fields.object failure)
   (<status> ^performed.action-event.type describe)
}

sp {object-is-question-rel*propose*process-svs-question
   (state <s> ^interaction.stack.segment <seg>)
   (<seg> ^purpose <p>
          ^info.obj1 <id1>
          ^info.obj2 <id2>)
   (<p> ^type object-is-question-rel
        ^parameters.prep <prep>)
-->
   (<s> ^operator <o> +)
   (<o> ^name process-svs-is-question
        ^category svs
	    ^message <m>)
   (<m> ^fields <f> ^type svs-is-query)
   (<f> ^objects <object>)
   (<object> ^obj <obja>
             ^obj <objb>
             ^type <prep>)
   (<obja> ^id <id1> ^index 1)
   (<objb> ^id <id2> ^index 2)
}

