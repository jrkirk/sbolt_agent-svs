sp {propose*push-object-is-question-rel
   (state <s> ^topstate <s>
              ^instructor-message <msg>)
   (<msg> ^type object-is-question
          ^information.relation.questioned true)
-->
   (<s> ^operator <o> +)
   (<o> ^name push-object-is-question-rel
        ^category communication
        ^message <msg>)
}

sp {push-object-is-question-rel*elaborate*empty*true
   (state <s> ^operator <o> +)
   (<o> ^name push-object-is-question-rel
        ^message.information.relation.{<< p1 p2 >>}.object <obj>)
   (<obj> -^word <> UNKNOWN)
-->
   (<obj> ^empty true)
}

sp {push-object-is-question-rel*elaborate*empty*false
   (state <s> ^operator <o> +)
   (<o> ^name push-object-is-question-rel
        ^message.information.relation.{<< p1 p2 >>}.object <obj>)
   (<obj> ^word <> UNKNOWN)
-->
   (<obj> ^empty false)
}

sp {apply*push-object-is-question-rel
   (state <s> ^operator <o>)
   (<o> ^name push-object-is-question-rel
        ^message <msg>)
   (<msg> ^type object-is-question
          ^information.relation.questioned true
          ^information.relation <rel>)
   (<rel> ^p1.object <obja> ^p2.object <objb> ^word <prep>)
-->
   (<msg> ^purpose <p>)
   (<p> ^type object-is-question-rel
        ^parameters <params>
        ^satisfaction <sat>)
   (<params> ^object1 <obja>
             ^object2 <objb>
             ^prep <prep>)
   (<sat> ^action-event.type describe)
}