
sp {initialize*filter-list
   (state <s> ^superstate nil)
-->
   (<s> ^filters <f>)
   (<f> ^filter x-aligned ^filter x-greater-than ^filter x-less-than
        ^filter y-aligned ^filter y-greater-than ^filter y-less-than
        ^filter z-aligned ^filter z-greater-than ^filter z-less-than)
}

sp {svs*elaborate*svs*objects*ids*to*int 
   (state <s> ^superstate nil
   	          ^svs.spatial-scene <sc>)
   (<sc> ^child <C>)
   (<C> ^id <obja1>) 
 --> 
   (<C> ^id-int (int <obja1>))
 }

sp {clean-up*extracts*after*svs
   (state <s> ^operator.name pop-segment
              ^superstate nil
              ^svs.command <cmd>)
   (<cmd> ^extract <E11>)
   (<E11> ^a <N7> ^b <N8>)
-->
   (<cmd> ^extract <E11> -)
}
sp {clean-up*project*after*svs
   (state <s> ^operator.name pop-segment
              ^superstate nil
              ^svs.command <cmd>)
   (<cmd> ^project <P11>)
-->
   (<cmd> ^project <P11> -)
}
sp {clean-up*object1*after*svs
   (state <s> ^operator.name pop-segment
              ^topstate <s>
              ^obj2 <o2>)
-->
   (<s> ^obj2 <o2> -)
}

sp {clean-up*object2*after*svs
   (state <s> ^operator.name pop-segment
              ^topstate <s>
              ^obj1 <o1>)
-->
   (<s> ^obj1 <o1> -)
}

sp {extract-filters*for-query*first
   (state <s> ^name svs-query
              ^topstate.filters.filter <fil>
              ^topstate.svs.spatial-scene <sc>
              ^topstate.svs.command <cmd>
              ^superstate.operator.message.fields.objects <objs>)
   (<objs> ^obj <object>
           ^type <prep>)
   (<object> ^id <id> ^index 1)
   (<sc> ^child <C>)
   (<C> ^id <obja> ^id-int <id>) 
-->
   (<cmd> ^extract <E11>)
   (<E11> ^a <N7> ^b <N8> ^type <fil>)
   (<N7> ^type node ^name <obja>)
   (<N8> ^type all_nodes)
}

sp {extract-filters*for-query*second
   (state <s> ^name svs-query
              ^topstate.filters.filter <fil>
              ^topstate.svs.spatial-scene <sc>
              ^topstate.svs.command <cmd>
              ^superstate.operator.message.fields.objects <objs>)
   (<objs> ^obj <object>
           ^type <prep>)
   (<object> ^id <id> ^index 2)
   (<sc> ^child <C>)
   (<C> ^id <obja> ^id-int <id>) 
-->
   (<cmd> ^extract <E11>)
   (<E11> ^a <N7> ^b <N8> ^type <fil>)   
   (<N7> ^type all_nodes)
   (<N8> ^type node ^name <obja>)
}

sp {extract-filters*for-labeling
   (state <s> ^name svs-label
              ^topstate.filters.filter <fil>
              ^topstate.svs.command <cmd>
              ^topstate.svs.spatial-scene <sc>
              ^superstate.operator.message.fields.objects <objs>)
   (<objs> ^type <type>
           ^obj <oa>
           ^obj <ob>)
   (<ob> ^index <i2> ^id <objb>)
   (<oa> ^index <i1> < <i2>
         ^id <obja>)   
   (<sc> ^child <C> ^child <C2>)
   (<C> ^id <obja1> ^id-int <obja>) 
   (<C2> ^id <objb1> ^id-int <objb>)
-->
   (<cmd> ^extract <E11>)
   (<E11> ^a <N7> ^b <N8> ^type <fil>)
   (<N7> ^type node ^name <obja1>)
   (<N8> ^type node ^name <objb1>)
}



#sp {extractsPA
#   (state <s> ^superstate nil
#              ^svs.command <cmd>)
#-->
#   (<cmd> ^extract <E11>)
#   (<E11> ^a <N7> ^b <N8> ^type planar-aligned)
#   (<N7> ^type all_nodes)
#   (<N8> ^type all_nodes)}
