## propose measure for all objects with an unknown measured property
sp {propose*measure*object
   (state <s> ^object <obj>
              ^io.input-link.objects.object <il-obj>
   	       	-^problem-space << lgsoar message-interpretation >>
	      ^name {<any> <> evaluate-operator})
   (<il-obj> ^id <id>
             ^measurable-prop.category <cat-name>)
   (<obj> ^category block
          ^id <id>)
   -{(<obj> ^property <prop>)
     (<prop> ^name <cat-name>
             ^active true)}
 -->
   (<s> ^operator <op> +)
   (<op> ^name op_measure-object
   	 ^argument1 <obj>
     ^category primitive-action
     ^kind non-atomic)
}

# If the property disappears from the input-link don't remove the property, 
#   but remove the active flag
sp {apply*operator*remove*active
   (state <s> ^operator <o>
              ^object <obj>
              ^io.input-link.objects.object <il-object>)
   (<obj> ^id <id>
          ^property <prop>)
   (<prop> ^name <cat-name>
           ^type measurable
           ^active true)
   (<il-object> ^id <id>
               -^measurable-prop.category <cat-name>)
-->
   (<prop> ^active true -)
}

# Creates a property structure on the object
sp {apply*measure*create*property
   (state <s> ^operator <o>
              ^io.input-link.objects.object <il-obj>)
   (<il-obj> ^id <id>
             ^measurable-prop <m-prop>)
   (<m-prop> ^category <cat-name>)
   (<o> ^name op_measure-object
        ^argument1 <obj>)
   (<obj> ^id <id>
         -^property.name <cat-name>)
-->
   (<obj> ^property <prop>)
   (<prop> ^name <cat-name>
           ^type measurable)
}

# If there is a new value on the input-link copy it
sp {apply*measure*create*value
   (state <s> ^operator <o>
              ^io.input-link.objects.object <il-obj>)
   (<il-obj> ^id <id>
             ^measurable-prop <m-prop>)
   (<m-prop> ^category <cat-name>
             ^{<label> <> category <> unknown <> features} <conf>)
   (<o> ^name op_measure-object
        ^argument1 <obj>)
   (<obj> ^id <id>
          ^property <prop>)
   (<prop> ^name <cat-name>
          -^value.label <label>)
-->
   (<prop> ^value.label <label>)
}

# If there is no longer a certain value on the input-link remove it from WM
sp {apply*measure*delete*value
   (state <s> ^operator <o>
              ^io.input-link.objects.object <il-obj>)
   (<il-obj> ^id <id>
             ^measurable-prop <m-prop>)
   (<m-prop> ^category <cat-name>
            -^<label>)
   (<o> ^name op_measure-object
        ^argument1 <obj>)
   (<obj> ^id <id>
          ^property <prop>)
   (<prop> ^name <cat-name>
           ^value <value>)
   (<value> ^label <label>)
-->
   (<prop> ^value <value> -)
}

# Mark the property active
sp {apply*measure*mark*active
   (state <s> ^operator <o>
              ^io.input-link.objects.object <il-obj>)
   (<il-obj> ^id <id>
             ^measurable-prop.category <cat-name>)
   (<o> ^name op_measure-object
        ^argument1 <obj>)
   (<obj> ^id <id>
          ^property <prop>)
   (<prop> ^name <cat-name>)
-->
   (<prop> ^active true)
}  