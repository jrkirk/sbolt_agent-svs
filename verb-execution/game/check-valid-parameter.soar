sp {check-valid-parameter*propose*find-relations*a
   (state <s> ^name check-valid-parameter
              ^arg <arg>
              ^obj <obj>
              ^found-prep <fprep>
              ^cond <cond>)
   (<cond> ^prep <prep>)
   (<obj> ^id <id>)
   (<prep> ^name <name> ^a this)
 -{(<s> ^relationships <rels>)
   (<rels> ^with <id> ^preposition <name>)}
-->
   (<s> ^operator <o> + =)
   (<o> ^name find-relations
        ^category svs
        ^obj <id>
        ^ind 2
        ^found-prep <fprep>
        ^prep <name>) 
}

sp {check-valid-parameter*propose*describe-object
   (state <s> ^name check-valid-parameter
              ^obj <obj>
             -^described <obj>
              ^cond <cond>)
   (<cond> ^attribute <a>)
   (<obj> ^id <id>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name describe-object
        ^category svs
        ^object <obj>)
}

sp {check-valid-parameter*apply*describe-object
   (state <s> ^name check-valid-parameter
              ^objd <objd>
              ^operator <o>)
   (<o> ^name describe-object
        ^object <obj>
        ^description <desc>)
-->
   (<s> ^described <obj>)
   (<objd> ^desc <desc> ^object <obj>)
}

sp {check-valid-parameter*check-attribute*propose*complete-valid-parameter
   (state <s> ^name check-valid-parameter
              ^obj <obj>
              ^objd <objd>
              ^cond <cond>)
   (<objd> ^object <obj> ^desc <desc>)
   (<cond> ^attribute.<a> <word>)
   (<desc> ^adjective <word>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name complete-valid-parameter
        ^category svs) 
}

sp {check-valid-parameter*check-attribute*propose*complete-invalid-parameter
   (state <s> ^name check-valid-parameter
              ^obj <obj>
              ^objd <objd>
              ^cond <cond>)
   (<objd> ^object <obj> ^desc <desc>)
   (<cond> ^attribute.<a> <word>)
   (<desc> -^adjective <word>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name complete-invalid-parameter
        ^category svs) 
}

sp {check-valid-parameter*check-relation*negative-all*propose*complete-valid-parameter
   (state <s> ^name check-valid-parameter
              ^arg <arg>
              ^obj <obj>
              ^relationships <rels>
              ^cond <conda>)
   (<rels> ^with <id> ^preposition <name> ^failure none-found)
   (<conda> ^prep <prep>)
   (<obj> ^id <id>)
   (<prep> ^name <name> ^a this ^b all ^negative true)
-->
   (<s> ^operator <o> + =)
   (<o> ^name complete-valid-parameter
        ^category svs) 
}

sp {check-valid-parameter*check-relation*negative-all*propose*complete-invalid-parameter
   (state <s> ^name check-valid-parameter
              ^arg <arg>
              ^obj <obj>
              ^relationships <rels>
              ^cond <conda>)
   (<rels> ^with <id> ^preposition <name> ^relations.matched true)
   (<conda> ^prep <prep>)
   (<obj> ^id <id>)
   (<prep> ^name <name> ^a this ^b all ^negative true)
-->
   (<s> ^operator <o> + =)
   (<o> ^name complete-invalid-parameter
        ^category svs) 
}

sp {check-valid-parameter*apply*complete-valid-parameter
   (state <s> ^name check-valid-parameter
              ^arg <arg>
              ^obj <obj>
              ^superstate <ss>
              ^operator.name complete-valid-parameter)
-->
   (<ss> ^valid-arg.<arg> <obj>)
}

sp {check-valid-parameter*apply*complete-invalid-parameter
   (state <s> ^name check-valid-parameter
              ^arg <arg>
              ^obj <obj>
              ^superstate <ss>
              ^operator.name complete-invalid-parameter)
-->
   (<ss> ^invalid-arg.<arg> <obj>)
}