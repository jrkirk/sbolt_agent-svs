
sp {find-parameter*propose*index-object
   (state <s> ^name find-parameter
             -^obj <objs>
              ^objd <objd>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name index-object
        ^return-all true
        ^category svs #object-processing
	    ^object <objd>)
}

sp {find-parameter*apply*index-object
   (state <s> ^name find-parameter
              ^operator <o>
              ^atr <atr>
              ^param <param>
              ^topstate.object <obj>
              ^objd <objd>)
   (<o> ^name index-object
        ^match.id <id>)
   (<obj> ^id <id> ^<atr> <param>)
-->
   (<s> ^obj <obj>)
}

sp {find-parameter*propose*lookup-preposition
   (state <s> ^name find-parameter
             -^found-prep.type <name>
              ^cond <conda>)
   (<conda> ^prep <prep>)
   (<prep> ^name <name> ^a this)
-->
   (<s> ^operator <o> + =)
   (<o> ^name lookup-preposition
        ^category svs
        ^type <name>)
}

sp {find-parameter*propose*find-relations
   (state <s> ^name find-parameter
              ^arg <arg>
              ^obj <obj>
              ^found-prep <fprep>
              ^cond <cond>)
   (<fprep> ^type <name>)
   (<cond> ^prep <prep>)
   (<obj> ^id <id>)
   (<prep> ^name <name> ^a this)
 -{(<s> ^relationships <rels>)
   (<rels> ^with <id> ^preposition <name>)}
-->
   (<s> ^operator <o> + =)
   (<o> ^name find-relations
        ^category svs
        ^obj <id>
        ^ind 1
        ^found-prep <fprep>
        ^prep <name>) 
}

sp {find-parameter*check-relation*negative-all*propose*found-possible-prep-match
   (state <s> ^name find-parameter
              ^arg <arg>
              ^atr <atr>
              ^obj <obj>
              ^topstate <ts>
              ^relationships <rels>
              ^cond <conda>)
   (<rels> ^with <id> ^preposition <name>) #)^failure none-found)
   (<conda> ^prep <prep>)
   (<obj> ^id <id>)
   (<prep> ^name <name> ^a this ^b << INDEF DEF >> ^negative true ^btype <btype>)
 -{(<rels> ^relations <r>)
   (<r> ^matched true ^obj <obj-id>)
   (<ts> ^object <o1>)
   (<o1> ^id <obj-id> ^<atr> <btype>)}
 -{(<s> ^found-possible <fp>)
   (<fp> ^prep <prep> ^obj <obj>)}
-->
   (<s> ^operator <o> + =)
   (<o> ^name found-possible-prep-match
        ^obj <obj>
        ^prep <prep>
        ^category svs)
}

sp {find-parameter*check-relation*positive-one*propose*found-possible-prep-match
   (state <s> ^name find-parameter
              ^arg <arg>
              ^obj <obj>
              ^atr <atr>
              ^relationships <rels>
              ^topstate.object <tobj>
              ^cond <conda>)
   (<rels> ^with <id> ^preposition <name> ^relations <r>)
   (<r> ^matched true ^obj <obj-id>)
   (<conda> ^prep <prep>)
   (<obj> ^id <id>)
   (<tobj> ^id <obj-id> ^<atr> <btype>)
   (<prep> ^name <name> ^a this ^b << INDEF DEF >> ^negative false ^btype <btype>)
 -{(<s> ^found-possible <fp>)
   (<fp> ^prep <prep> ^obj <obj>)}
-->
   (<s> ^operator <o> + =)
   (<o> ^name found-possible-prep-match
        ^obj <obj>
        ^prep <prep>
        ^category svs) 
}


sp {find-parameter*check-relation*apply*found-possible-prep-match
   (state <s> ^name find-parameter
              ^arg <arg>
              ^obj <obj>
              ^atr <atr>
              ^operator <o>)
   (<o> ^name found-possible-prep-match
        ^obj <obj>
        ^prep <prep>)
-->
   (<s> ^found-possible <fp>)
   (<fp> ^prep <prep> ^obj <obj>)
}
sp {find-parameter*check-relation*propose*complete-find-parameter
   (state <s> ^name find-parameter
              ^arg <arg>
              ^obj <obj>
              ^atr <atr>
              ^obj <obj>
#             -^valid-arg.<arg> <obj>
              ^cond <cond>)
  -{(<s> ^found-prep <fprep>)
   (<fprep> -^obj <obj>)}
-->
   (<s> ^operator <o> + =)
   (<o> ^name complete-find-parameter
        ^obj <obj>
        ^category svs)
}

sp {find-parameter*apply*complete-find-parameter
   (state <s> ^name find-parameter
              ^arg <arg>
              ^superstate <ss>
              ^operator <o>)
   (<o> ^name complete-find-parameter
        ^obj <obj>)
-->
   (<ss> ^valid-arg.<arg> <obj>)
#   (<s> ^valid-arg.<arg> <obj>)
}

#handle no prep
sp {find-parameter*no-prep-condition*propose*complete-find-parameter
   (state <s> ^name find-parameter
              ^arg <arg>
              ^obj <obj>
#             -^valid-arg.<arg> <obj>
             -^cond.prep)
-->
   (<s> ^operator <o> + =)
   (<o> ^name complete-find-parameter
        ^obj <obj>
        ^category svs) 
}
#default
sp {find-parameter*propose*complete-find-parameter-failed
   (state <s> ^name find-parameter
              ^arg <arg>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name complete-find-parameter-failed
        ^category svs) 
}

#todo failure handle better
sp {find-parameter*apply*complete-find-parameter-failed
   (state <s> ^name find-parameter
              ^arg <arg>
              ^superstate <ss>
              ^operator.name complete-find-parameter-failed)
-->
   (<ss> ^valid-arg.<arg> failed)
}