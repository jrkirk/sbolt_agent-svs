sp {lookup-general*query
   (state <s> ^name lookup-general
              ^superstate.operator.game-name <name>
              ^smem.command <sc>)
-->
   (<sc> ^query <q>)
   (<s> ^found-game <game>)
   (<game> ^name <name>)
   (<q> ^game-name <name>)
}

sp {lookup-general*query*success
   (state <s> ^name lookup-general
              ^superstate.operator.game-name <name>
              ^smem.result.success <cue>
              ^smem.result <res>)
   (<res> ^retrieved <ret>)
-->
   (<s> ^new <ret>
        ^top <ret>)
}

sp {lookup-general*query*failure
   (state <s> ^name lookup-general
              ^superstate.operator.game-name <name>
              ^smem.result.failure <cue>)
-->
   (<s> ^failure true)
}

sp {lookup-general*propose*lookup-next-level
   (state <s> ^name lookup-general
              ^new <ret>
             -^tried <lte>)
   (<ret> ^<arg> <lte>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name lookup-next-level
        ^category svs
        ^lte <lte>)
}

sp {lookup-general*propose*complete-lookup-general
   (state <s> ^name lookup-general
              ^top <top>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name complete-lookup-general
        ^category svs
        ^top <top>)
}

sp {lookup-general*propose*complete-lookup-general*failure
   (state <s> ^name lookup-general
              ^failure true)
-->
   (<s> ^operator <o> + =)
   (<o> ^name complete-lookup-general
        ^category svs
        ^failure true)
}

sp {lookup-general*apply*complete-lookup-general
   (state <s> ^name lookup-general
              ^operator <o>
              ^superstate <ss>
              ^top <top>)
   (<o> ^name complete-lookup-general
        ^top <top>)
-->
   (<ss> ^retrieved <top>)
}

sp {lookup-general*apply*complete-lookup-general*failure
   (state <s> ^name lookup-general
              ^operator <o>
              ^superstate <ss>)
   (<o> ^name complete-lookup-general
        ^failure true)
-->
   (<ss> ^failed true)
}

sp {lookup-smem*lookup-next-level*retrieve-lte
   (state <s> ^name lookup-next-level
              ^smem.command <sc>
              ^superstate.operator.lte <lte>)
-->
   (<sc> ^retrieve <lte>)
}

sp {lookup-smem*lookup-next-level*retrieved
   (state <s> ^name lookup-next-level
              ^smem.result <res>
              ^superstate <ss>
              ^smem.result.success
              ^superstate.operator.lte <lte>)
   (<res> ^retrieved <ret>)
-->
   (<ss> ^new <ret>
         ^tried <lte>)
}

sp {lookup-smem*lookup-next-level*retrieved*fail
   (state <s> ^name lookup-next-level
              ^smem.result <res>
              ^superstate <ss>
              ^smem.result.failure
              ^superstate.operator.lte <lte>)
-->
   (<ss> ^tried <lte>)
}

sp {lookup-smem*lookup-next-level*retrieved*fail2
   (state <s> ^name lookup-next-level
              ^superstate <ss>
              ^smem.result.bad-cmd
              ^superstate.operator.lte <lte>)
-->
   (<ss> ^tried <lte>)
}
