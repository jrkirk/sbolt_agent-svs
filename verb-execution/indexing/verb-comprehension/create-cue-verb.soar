### 
sp {create*cue*verb
   (state <s> ^name create-cue
   	      ^superstate.information <info>)
-->
   (<s> ^information <info>
   	^bit flip)
}

sp {elaborate*verb
   (state <s> ^name create-cue
   	      ^information <info>)
   (<info> ^verb.word <verb>)
-->
   (<s> ^cue <cue>)
   (<cue> ^verb <verb>)
}

sp {elaborate*arguments*direct-object
   (state <s> ^name create-cue
   	      ^information.verb <info>)
   (<info> ^direct-object.object <do>)
-->
   (<s> ^argument <do>)
}

sp {elaborate*arguments*pp-object
   (state <s> ^name create-cue
   	      ^information.verb <info>)
   (<info> ^preposition.object <do>)
-->
   (<s> ^argument <do>)
}

sp {propose*index-argument*direct-object
   (state <s> ^name create-cue
   	      ^information.verb <info>
	      ^cue <cue>
	      -^matched <do>
	      ^bit flip)
   (<info> ^direct-object.object <do>)
   (<cue> -^direct-object <any>)
-->
   (<s> ^operator <op> + =)
   (<op> ^name index-argument
   	 ^cue <cue>
	 ^connector direct-object
	 ^argument <do>
	 ^category indexing)
}


sp {propose*index-argument*preposition
   (state <s> ^name create-cue
   	      ^information.verb <info>
	      ^bit flip
	      -^_relation.preposition <prep>)
   (<info> ^preposition.word <prep>)
  -->
   (<s> ^operator <op> + =)
   (<op> ^name index-preposition
	 ^preposition <prep>
	 ^category indexing)
}

sp {propose*index-argument*preposition*object
   (state <s> ^name create-cue
   	      ^information.verb <info>
	      ^cue <cue>
	      -^matched <obj>
	      ^bit flip
	      ^_relation <rel>)
   (<info> ^preposition.object <obj>)
   (<rel> ^preposition <p>)
   (<cue> -^<p> <any>)
-->
   (<s> ^operator <op> + =)
   (<op> ^name index-argument
   	 ^cue <cue>
	 ^argument <obj>
	 ^connector <p>
	 ^category indexing)
}

### mark success if only verb
sp {elaborate*verb*copy-success
   (state <s> ^name create-cue
   	      ^information.verb <info>
	      ^superstate <ss>
	      ^cue <cue>)
   (<info> ^word <verb>
   	   -^direct-object <any>
	   -^preposition <any2>)
-->
   (<ss> ^cue <cue>)
}

### mark success if other attributes
sp {elaborate*copy-success*match-complete
   (state <s> ^name create-cue)
   (<s> ^argument <obj>
  	-^matched <obj>)
-->
   (<s> ^match not-complete)
}

sp {write-to-superstate*copy-success*match-complete
   (state <s> ^name create-cue
   	      -^match not-complete
	      ^cue <cue>
	      ^matched <arg>
	      ^superstate <ss>
	      ^information.verb <info>)
-->
   (<ss> ^cue <cue>)
}

sp {write-to-superstate*copy-success*match-complete*spatial-relationship
   (state <s> ^name create-cue
   	      -^match not-complete
	      ^_relation <rel>
	      ^matched <arg>
	      ^superstate <ss>
	      ^information.verb <info>)
-->
   (<ss> ^_relation <rel>)
}
   
   


