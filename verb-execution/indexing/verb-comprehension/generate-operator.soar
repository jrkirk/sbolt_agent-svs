#### generates a grounded operator
sp {elaborate*generate*operator
   (state <s> ^name generate-operator
   	      ^superstate.operator <sop>)
   (<sop> ^procedural-id <pid>
   	  ^linguistic-id <lid>
	  ^map-id <map>
	  ^cue <cue>)
-->
   (<s> ^procedural-id <pid>
   	^linguistic-id <lid>
	^map-id <map>
	^cue <cue>)
}

sp {generate*operator*name
   (state <s> ^name generate-operator
   	      ^procedural-id <pid>)
   (<pid> ^op_name <op-name>)
-->
   (<s> ^index-operator <op>)
   (<op> ^name <op-name>)
 }

sp {elaborate*spatial-relationship*generate-operator
   (state <s> ^name generate-operator
   	      ^superstate._relation <rel>)
-->
   (<s> ^_relation <rel>)  
}

sp {generate*operator*arguments
   (state <s> ^name generate-operator
   	      ^procedural-id <pid>
	      ^linguistic-id <lid>
	      ^cue <cue>
	      ^index-operator <op>)
   (<cue> ^<connect> <obj>)
   (<lid> ^<connect> <lobj>)
   (<pid> ^<arg> <lobj>)
-->
   (<op> ^<arg> <obj>)
}

sp {generate*operator*arguments*metadata
   (state <s> ^name generate-operator
   	      ^procedural-id <pid>
	      ^linguistic-id <lid>
	      ^cue <cue>
	      ^index-operator <op>
	      ^topstate.interaction.stack.segment.info.index-object-context <context>)
   (<op> ^_metadata <met>)
   (<lid> ^<connect> <lobj>)
   (<pid> ^<arg> <lobj>)
   (<context> ^context-id <connect>)
-->
   (<met> ^arg-data <a1>)
   (<a1> ^argument <arg>
   	 ^description <connect>
	 ^context-id <connect>
	 ^arg-context <context>)
 
}

#### associate spatial-relationships
sp {associate*spatial*relations
   (state <s> ^name generate-operator
   	      ^procedural-id <pid>
	      ^linguistic-id <lid>
	      ^index-operator <op>
	      ^cue <cue>
	      ^_relation <rel>)
   (<lid> ^<connect> <obj>)
   (<cue> ^<connect> <lobj>)
-->
   (<op> ^_relation <rel>)
}

sp {copy*map-id*metadata
   (state <s> ^name generate-operator
   	      ^index-operator <op>
	      ^map-id <map>)
-->
   (<op> ^_metadata <met>)
   (<met> ^map-id <map>)
}

sp {generate*operator*arguments*write-to-superstate
   (state <s> ^name generate-operator
   	      ^index-operator <op>
	      ^superstate <ss>)
  (<op> ^_metadata <any>) #JK removed check for arg-data because have no context
-->
   (<ss> ^indexed-operator <op>)
}

### elaborate spatial relationship others
sp {prefer*match*epmem*elaborate*spatial-rel*indexed-operator
   (state <s> ^name selection
   	      ^operator <o>
	      ^superstate.desired <des>
	      ^indexed-operator <iop>)
   (<o> -^not-matched <any>
   	^superoperator <sop>)
   (<iop> ^_relation._ltid <prep>)
 -{(<des> ^track-relation <tr>)
   (<tr> ^spatial-relationship <prep>
   	 ^p1 <val3>
	 ^p2 <val4>)
   (<sop> ^<arg1> <val1>
   	  ^<arg2> <val2>)
   (<val1> ^focus <fval1>)
   (<val2> ^focus <fval2>)
   (<val3> ^focus <fval1>)
   (<val4> ^focus <fval2>)}
-->
   (<sop> ^spatial-relationship <prep>)
}

