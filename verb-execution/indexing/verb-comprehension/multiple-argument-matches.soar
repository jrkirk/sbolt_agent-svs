### propose resolve-multiple-matches-dialog for op ties even after procedural constraints
#### process multiple matches to one argument, rely on interaction
sp {multiple*argument*matches*create*operator
   (state <s> ^impasse tie
	      ^item.<arg> <val1>
	      ^item.<arg> {<> <val2> <val1>}
	      -^learning-operator <any>
	      -^status.type which-question
	      ^superstate <ss>)
   (<ss> ^indexed-operator <iop>
	 ^resolve-operator <op>)
   (<iop> ^_metadata <met>)   
   (<met> ^arg-data <a1>)
   (<a1> ^arg-context <con>
   	 ^argument <arg>
	 ^context-id <conid>)
   (<con> ^object <obj>)
-->
   (<op> ^name resolve-multiple-matches-dialog 
   	 ^object-desc <obj>
	 ^indexed-operator <iop>
	 ^context-id <conid>
	 ^category indexing)
}

sp {multiple*argument*matches*create*operator*composite-operator
   (state <s> ^superstate <ss>
   	     -^learning-operator <any>
             -^status.type which-question)
   (<ss> ^indexed-operator <iop>
	 ^resolve-operator <rop>
	 ^operator.category composite-action)
   (<iop> ^<arg> <val1>
	  ^<arg> {<val2> <> <val1>}
	  ^_metadata <met>)	
   (<met> ^arg-data <a1>)
   (<a1> ^arg-context <con>
   	 ^argument <arg>
	 ^context-id <conid>)
   (<con> ^object <obj>)
-->
   (<rop> ^name resolve-multiple-matches-dialog 
   	  ^object-desc <obj>
	  ^indexed-operator <iop>
	  ^category indexing
	 ^context-id <conid>)
   (<ss> ^operator <iop> -)
}

sp {propose*resolve-multiple-matches-dialog
   (state <s> ^resolve-operator <op>)
   (<op> ^category)
-->
   (<s> ^operator <op> +)
}

sp {elaborate*state*resolve-multiple-matches-dialog
   (state <s> ^superstate.operator <sop>) 
   (<sop> ^object-desc <obj>
   	  ^indexed-operator <iop>
	  ^context-id <conid>)
-->
   (<s> ^object-desc <obj>
   	^indexed-operator <iop>
	^name complete-index-object
	^context-id <conid>)
}

sp {multiple*argument*match*propose*which-question
   (state <s> ^name complete-index-object
   	      ^indexed-operator <iop>
	      ^object-desc <obj>
	      ^context-id <conid>)
-->
   (<s> ^operator <op> + =)
   (<op> ^name which-question
   	 ^object <obj>
	 ^context-id <conid>)
}

#### delete the context-id not related to the which-question
sp {which-question*unrelated*context-id
   (state <s> ^topstate.interaction.stack.segment.info <info>
   	      ^operator <op>)
   (<op> ^name which-question
   	 ^context-id <conid>)
   (<info> ^index-object-context <object-context>)
   (<object-context> ^context-id <id> {<> <id> <conid>})
-->
   (<info> ^index-object-context <object-context> -)
}


##### cleanup

### delete the indexed operator 
sp {multiple*argument*match*delete*indexed-operator
   (state <s> ^operator.name which-question
   	      ^topstate.interaction.stack.segment.purpose <p>)
   (<p> ^indexed-operator <op>)
-->
   (<op> ^clear true)
}

sp {multiple*argument*match*clear*indexed-operator
   (state <s> ^topstate <s>
   	      ^interaction.stack.segment.purpose <p>
	      ^operator.name push-segment)
   (<p> ^indexed-operator <op>)
   (<op> ^clear true)
-->
   (<p> ^indexed-operator <op> -)
}

### remove resolve-operator during push-segment
sp {remove*resolve-operator
   (state <s> ^resolve-operator <rop>
   	      ^operator <op>)
   (<op> ^category << communication interaction-stack >>)
   (<rop> ^category indexing)
-->
   (<s> ^resolve-operator <rop> -
   	^resolve-operator <nrop>)
}
   	  


### preferences

## prefer which-question to primitives
sp {prefer*which-question*primitive-actions
   (state <s> ^operator <op1> +
   	      ^operator <op2> +)
   (<op1> ^name which-question)
   (<op2> ^category primitive-action)
-->
   (<s> ^operator <op2> -)
}

sp {prefer*which-question*evaluate
   (state <s> ^operator <op1> +
   	      ^operator <op2> +)
   (<op1> ^name which-question)
   (<op2> ^name << evaluate-operator >>)
-->
   (<s> ^operator <op2> -)
}

sp {prefer*multiple-matches*primitive-actions
   (state <s> ^operator <op1> +
   	      ^operator <op2> +)
   (<op1> ^name resolve-multiple-matches-dialog)
   (<op2> ^category << primitive-action composite-action >>)
-->
   (<s> ^operator <op1> > <op2>)
}

sp {prefer*resolve-multiple-matches-dialog*indicate-unresolved-tie
   (state <s> ^operator <op1> +
   	      ^operator <op2> +)
   (<op1> ^name resolve-multiple-matches-dialog)
   (<op2> ^name  indicate-unresolved-tie)
-->
   (<s> ^operator <op1> > <op2>)
}



