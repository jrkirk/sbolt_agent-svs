#### generates a grounded operator
sp {elaborate*generate*operator
   (state <s> ^name generate-operator
   	      ^superstate.operator <sop>)
   (<sop> ^procedural-id <pid>
   	  ^linguistic-id <lid>
	  ^cue <cue>)
-->
   (<s> ^procedural-id <pid>
   	^linguistic-id <lid>
	^cue <cue>)
}

sp {generate*operator*name
   (state <s> ^name generate-operator
   	      ^procedural-id <pid>)
   (<pid> ^op_name <op-name>)
-->
   (<s> ^index-operator <op>)
   (<op> ^name <op-name>)
}

sp {generate*operator*arguments
   (state <s> ^name generate-operator
   	      ^procedural-id <pid>
	      ^linguistic-id <lid>
	      ^cue <cue>
	      ^index-operator <op>)
   (<cue> ^<connect> <obj>)
   (<lid> ^<connect> <lobj>)
   (<pid> ^{<arg> <> spatial-relationship} <lobj>)
-->
   (<op> ^<arg> <obj>)
#   (<obj> ^_ltid <lobj>)
}

#### associate spatial-relationships
sp {associate*spatial*relations
   (state <s> ^name generate-operator
   	      ^procedural-id <pid>
	      ^linguistic-id <lid>
	      ^index-operator <op>
	      ^cue <cue>)
   (<pid> ^spatial-relationship <obj>)
   (<lid> ^<connect> <obj>)
   (<cue> ^<connect> <lobj>)
-->
   (<op> ^spatial-relationship <connect>)
}

sp {generate*operator*goal
   (state <s> ^name generate-operator
   	      ^procedural-id <pid>
	      ^index-operator <op>)
   (<pid> ^goal <g>)
-->
   (<op> ^goal-lid <g>)
}

sp {generate*operator*arguments*write-to-superstate
   (state <s> ^name generate-operator
   	      ^index-operator <op>
	      ^superstate <ss>)
   (<op> ^<> name <any>)
-->
   (<ss> ^indexed-operator <op>)
}
   