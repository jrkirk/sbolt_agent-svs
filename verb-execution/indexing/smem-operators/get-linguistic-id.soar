sp {get-linguistic-id*elaborate*state
   (state <s> ^name get-linguistic-id
   	      ^superstate.operator <sop>)
   (<sop> ^name get-linguistic-id
   	  ^cue <cue>
	  -^failure specific-relation)
-->
   (<s> ^cue <cue>
   	^original-cue <cue>)
}

sp {get-linguistic-id*elaborate*smem*command
   (state <s> ^name get-linguistic-id
   	      ^smem.command <scom>
	      ^cue <cue>)
 -->
   (<scom> ^query <cue>)
}


sp {get-linguistic-id*copy*original*cue
   (state <s> ^name get-linguistic-id
   	      ^superstate.original-cue <oc>)
 -->
   (<s> ^original-cue <oc>)
}

sp {get-linguistic-id*write*to*superstate*no-failure
   (state <s> ^name get-linguistic-id
   	      ^smem.result <res>
	      ^superstate <ss>)
   (<res> ^retrieved <ret>)
   (<ss> ^operator <sop>)
   (<sop> -^failure specific-relation)
-->
   (<ss> ^linguistic <ret>)
}



sp {get-linguistic-id*write*to*superstate*recursive
   (state <s> ^name get-linguistic-id
   	      ^linguistic <ret>
	      ^superstate <ss>)
-->
   (<ss> ^linguistic <ret>)
}

#### if get-linguistic-id results in a failure, there is no mapping between language and procedural knowledge. Assume that this will occur in the following two cases, 1. the cue is overspecific, some primitive verbs such as put-down are designed to accept generic spatial relationship which is then passed to the operator, relax the cue and requery,

sp {get-linguistic-id*propose*relax-cue
   (state <s> ^name get-linguistic-id
   	      ^smem.result.failure
	      ^cue <cue>)
   (<cue> ^{<arg>  <> direct-object <> generic <> verb} <any>)
-->
   (<s> ^operator <op> +)
   (<op> ^name get-linguistic-id
   	 ^cue <cue>
	 ^failure specific-relation
	 ^relation <arg>
	 ^category indexing)
}

sp {get-linguistic-id*write*to*superstate*faliure
   (state <s> ^name get-linguistic-id
   	      ^smem.result <res>
	      ^superstate <ss>)
   (<res> ^retrieved <ret>)
   (<ss> ^operator <sop>)
   (<sop> ^failure specific-relation
   	  ^relation <arg>)
   (<ret> ^generic <sobj>)
-->
   (<ss> ^linguistic <ret>)
   (<ret> ^<arg> <sobj>)
}
	      
### if the reason of failure the use of a specific preposition, replace that by the word generic. 

sp {get-linguistic-id*elaborate*state*specific-relation*create*new*cue
   (state <s> ^name get-linguistic-id
   	      ^superstate.operator <sop>)
   (<sop> ^name get-linguistic-id
   	  ^cue <cue>
	  ^failure specific-relation
	  ^relation <rel>)
-->
   (<s> ^new-cue <ncue>)
}


sp {get-linguistic-id*elaborate*state*specific-relation*copy*other
   (state <s> ^name get-linguistic-id
   	      ^superstate.operator <sop>
	      ^new-cue <ncue>)
   (<sop> ^name get-linguistic-id
   	  ^cue <cue>
	  ^failure specific-relation
	  ^relation <rel>)
   (<cue> ^{<arg> <> <rel>} <val>)
-->
   (<ncue> ^<arg> <val>)
}

sp {get-linguistic-id*elaborate*state*specific-relation*copy*generic
   (state <s> ^name get-linguistic-id
   	      ^superstate.operator <sop>
	      ^new-cue <ncue>)
   (<sop> ^name get-linguistic-id
   	  ^cue <cue>
	  ^failure specific-relation
	  ^relation <rel>)
   (<cue> ^<rel> <val>)
-->
   (<ncue> ^generic <val>)
}

sp {get-linguistic-id*rewrite-cue
   (state <s> ^name get-linguistic-id
   	      ^new-cue <ncue>)
   (<ncue> ^generic)
-->
   (<s> ^cue <ncue>)
}


###  failing case 1 above, 2. the agent is learning a new action and a verb associated with it. Create new mapping in semantic memory and return that.

sp {get-linguistic-id*failure
   (state <s> ^name get-linguistic-id
   	      ^smem.result <res>
	      ^original-cue <cue>)
   (<res> ^failure <any>)
-->
   (<s> ^operator <op> +)
   (<op> ^name create-new-mapping
   	 ^category indexing
	 ^cue <cue>)}

sp {get-linguistic-id*failure*write*to*superstate
   (state <s> ^name get-linguistic-id
   	      ^new-mapping <map>
	      ^superstate <ss>)
   (<map> ^linguistic <ling>)
-->
  (<ss> ^linguistic <ling>)
}
	      


### prefer get-linguistic-id over create-new-mapping
sp {prefer*get-linguistic-id*create-new-mapping
   (state <s> ^name get-linguistic-id
   	      ^operator <op1> +
	      ^operator <op2> +)
   (<op1> ^name get-linguistic-id)
   (<op2> ^name create-new-mapping)
-->
   (<s> ^operator  <op1> > <op2>)
}
   	 