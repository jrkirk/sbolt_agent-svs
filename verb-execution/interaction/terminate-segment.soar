### terminate an open segment on the interaction stack if the purpose of the segment is satisfied


### the segment was originated by the agent and a dialog-event was expected from the instructor

sp {propose*pop-segment*agent
   (state <s> ^interaction.stack.segment <segment>
              ^instructor-message <m>)
   (<segment> ^purpose.satisfaction <sat>
   	      ^originator agent)
   (<sat> ^dialog-event <etype>)
   (<m> ^type <etype>)
-->
   (<s> ^operator <op> +)
   (<op> ^name pop-segment
   	 ^segment <segment>
	 ^category interaction-stack)
}


### the segment was originated by the instructo and an action-event/learning-event was expected from the agent

sp {propose*pop-segment*instructor
   (state <s> ^interaction.stack.segment <segment>
   	      ^status.performed.<etype> <info>)
   (<segment> ^purpose.satisfaction <sat>
   	      ^originator instructor)
   (<sat> ^<etype> <info>)
-->
   (<s> ^operator <op> +)
   (<op> ^name pop-segment
   	 ^segment <segment>
	 ^category interaction-stack)
}

sp {propose*pop-segment*internal
   (state <s> ^interaction.stack.segment <segment>
   	      ^status.performed.<etype> <info>)
   (<segment> ^purpose.satisfaction <sat>
              ^originator agent)
   (<sat> ^<etype> <info>)
-->
   (<s> ^operator <op> +)
   (<op> ^name pop-segment
   	 ^segment <segment>
	 ^category interaction-stack)
}

sp {apply*pop-segment*instructor*clear
   (state <s> ^status <status>
   	      ^operator.name pop-segment)
-->
   (<s> ^status <status> -
   	^status <new-status>)
}