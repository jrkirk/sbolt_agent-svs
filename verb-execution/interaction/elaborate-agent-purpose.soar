### elaborate agent's purpose 

## case 1: agent does not know of any task to perform

sp {elaborate*agent-purpose*next-task-unknown
   (state <s> ^topstate <s>
   	      ^status <status>)
   (<status> ^type unresolved-tie
   	     ^state-name topstate)
-->
   (<status> ^purpose <p>)
   (<p> ^type get-next-task
        ^parameters.message.type get-next-task
   	^satisfaction <sat>)
    (<sat> ^dialog-event.type verb-command)
}

## case 2: agent does not know how to perform an action
sp {elaborate*agent-purpose*next-subaction
   (state <s> ^topstate <s>
              ^interaction.stack.segment.purpose.satisfaction.action-event.type.verb.word <verb>
   	      ^status <status>)
   (<status> ^type unresolved-tie
   	     -^state-name topstate)
-->
   (<status> ^purpose <p>)
   (<p> ^type get-next-subaction
        ^parameters.message <msg>
        ^context.verb <verb>
   	^satisfaction <sat>)
   (<sat> ^dialog-event.type verb-command
   	  ^dialog-event.type successful-completion)
   (<msg> ^type get-next-subaction
          ^fields.verb <verb>)
}

## case 3: agent does not know the goal of the verb
sp {elaborate*agent-purpose*get-goal
   (state <s> ^topstate <s>
              ^interaction.stack.segment.purpose.satisfaction.action-event.type.verb.word <verb>
   	      ^status <status>)
   (<status> ^type unknown-goal)
-->
   (<status> ^purpose <p>)
   (<p> ^type get-goal
        ^parameters.message.type get-goal
        ^context.verb <verb>
   	^satisfaction <sat>)
   (<sat> ^dialog-event <de>)
   (<de> ^type goal-relation-message)
}	     

sp {elaborate*agent-purpose*unreachable-goal
   (state <s> ^topstate <s>
   	      ^status <status>)
   (<status> ^performed.learning-event <e>)
   (<e> ^type explanation
   	^information failure)
-->
   (<status> ^purpose <p>)
   (<p> ^type restart-task-instruction
        ^parameters.message.type restart-task-instruction
   	^satisfaction <sat>)
        # AM: Changed the satisfaction structure
   (<sat> ^dialog-event.type verb-command)
}
