# Fetch the category for the given word from memory
sp {noun-information*propose*lookup-smem*category
   (state <s> ^name noun-information
              ^nounjective <nj>)
   (<nj> -^category
          ^word <word>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name lookup-smem
        ^category smem
        ^lookup-type category
        ^query <q>
        ^desired.category true)
   (<q> ^type word2cat ^word <word> ^category <cat>)
}

# Successful retrieval
sp {noun-information*apply*lookup-smem*category*success
   (state <s> ^name noun-information
              ^operator <o>
              ^nounjective <nj>)
   (<o> ^name lookup-smem
        ^lookup-type category
        ^query.word <word>
        ^result {<cat> <> failure})
   (<nj> ^word <word>)
-->
   (<nj> ^category <cat>)
}

# Failed retrieval
sp {noun-information*apply*lookup-smem*category*failure
   (state <s> ^name noun-information
              ^operator <o>
              ^nounjective <nj>)
   (<o> ^name lookup-smem
        ^lookup-type category
        ^query.word <word>
        ^result failure)
   (<nj> ^word <word>)
-->
   (<nj> ^category failure)
}