sp {noun-information*propose*complete-noun-information
   (state <s> ^name noun-information)
-->
   (<s> ^operator <o> +)
   (<o> ^name complete-noun-information
        ^category object-processing)
}

# Mark the action performed in the interaction stack
sp {noun-information*apply*complete-noun-information*word2cat
   (state <s> ^name noun-information
              ^operator.name complete-noun-information
              ^nounjective <cat-nj>
              ^nounjective <word-nj>
              ^topstate.instructor-message <msg>)
   (<cat-nj> ^category category
             ^word <cat>)
   (<word-nj> ^category failure
              ^word <word>)
-->
   (<msg> ^purpose <p>)
   (<p> ^type learn-word2cat
        ^parameters <params>
        ^satisfaction <sat>)
   (<params> ^category <cat>
             ^word <word>)
   (<sat> ^learning-event word2cat)
}

sp {noun-information*apply*complete-noun-information*all-known
   (state <s> ^name noun-information
              ^operator.name complete-noun-information
              ^superstate.operator <o>
              -^nounjective.category failure)
-->
   (<o> ^finished true)
}

sp {noun-information*apply*complete-noun-information*all-unknown
   (state <s> ^name noun-information
              ^operator.name complete-noun-information
              ^superstate.operator <o>
             -^nounjective.category <> failure)
-->
   (<o> ^finished failure)
}