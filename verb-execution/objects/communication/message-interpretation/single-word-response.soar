# Rules that handle when a single-word-response is sent by the user

sp {propose*push-single-word-response
   (state <s> ^topstate <s>
              ^instructor-message <msg>)
   (<msg> ^type single-word-response
         -^processed)
-->
   (<s> ^operator <o> +)
   (<o> ^name push-single-word-response
        ^category communication
        ^message <msg>)
}

# Elaborate useful information from the message to the operator
sp {elaborate*push-single-word-response*response
   (state <s> ^operator <o> +)
   (<o> ^name push-single-word-response
        ^message <msg>)
   (<msg> ^information.response <response>)
-->
   (<o> ^response <response>)
}

# teaching-requestion - respond with finished or cancel
sp {apply*push-single-word-response*teaching-request*remove*query
   (state <s> ^operator <o>
              ^instructor-message <msg>
              ^queries <qs>)
   (<o> ^name push-single-word-response
        ^response << finished cancel >>
        ^message <msg>)
   (<msg> ^query <q>)
-->
   (<qs> ^query <q> -)
   (<s> ^instructor-message <msg> -)
}

sp {apply*push-single-word-response*teaching-request*cancel
   (state <s> ^operator <o>)
   (<o> ^name push-single-word-response
        ^response cancel
        ^message.query <q>)
   (<q> ^object <obj>)
-->
   (<obj> ^no-teaching true)
}

#JK for prepositions
sp {apply*push-single-word-response*teaching-request*cancel*prep
   (state <s> ^operator <o>)
   (<o> ^name push-single-word-response
        ^response cancel
        ^message.query <q>)
   (<q> ^status <stat>)
-->
   (<stat> ^no-teaching true)
}

sp {apply*push-single-word-response*mark*processed
   (state <s> ^operator <o>)
   (<o> ^name push-single-word-response
        ^message <msg>)
-->
   (<msg> ^processed true)
}