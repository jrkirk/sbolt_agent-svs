sp {index-confirmation*propose*wait-for-arm
   (state <s> ^topstate <s>
              ^io.input-link.self <self>
              ^interaction.stack.segment <seg>)
   (<seg> ^purpose.type index-confirmation
          ^originator instructor)
   (<self> ^action <action> ^prev-action <prev>)
-->
   (<s> ^operator <o> +)
   (<o> ^name wait-for-arm)
}

sp {wait-for-arm*propose*wait-for-arm
   (state <s> ^name wait-for-arm
             -^operator.name wait-for-arm)
-->
   (<s> ^operator <o> +)
   (<o> ^name wait-for-arm)
}

sp {index-confirmation*apply*operator*mark*started-home
   (state <s> ^topstate <s>
              ^operator <o>
              ^io.input-link.self.action home
              ^interaction.stack.segment <seg>)
   (<seg> ^purpose.type index-confirmation
          ^originator instructor)
-->
   (<s> ^started-home true)
}

sp {index-confirmation*arm-finishes*mark-performed
   (state <s> ^topstate <s>
              ^operator.name wait-for-arm
              ^started-home true
              ^io.input-link.self.action wait
              ^interaction.stack.segment.purpose.type index-confirmation
              ^status <status>)
-->
   (<status> ^performed.action-event.type home)
   (<s> ^started-home true -)
}

sp {index-confirmation*arm-finishes*send-training
   (state <s> ^topstate <s>
              ^operator.name wait-for-arm
              ^started-home true
              ^io.input-link.self.action wait
              ^io.output-link <out>
              ^interaction.stack.segment.purpose <p>
              ^status <status>)
   (<p> ^type index-confirmation
        ^parameters <params>)
   (<params> ^object-id <id>
             ^object-desc.property <prop>
             ^response yes)
   (<prop> ^label <label>
           ^category <cat>)
-->
   (<out> ^send-training-label <stl>)
   (<stl> ^id <id> ^label <label> ^category <cat>)
}
