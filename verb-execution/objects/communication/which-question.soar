# Adds a segment to the interaction stack to deal with the which question

sp {propose*push-which-question
   (state <s> ^topstate <s>
              ^instructor-message <msg>)
   (<msg> ^type object-question
          ^information.question-word which)
-->
   (<s> ^operator <o> +)
   (<o> ^name push-which-question
        ^category communication)
}

sp {apply*push-which-question
   (state <s> ^operator.name push-which-question
              ^instructor-message <msg>)
   (<msg> ^information.object <desc>)
-->
   (<msg> ^purpose <p>)
   (<p> ^type which-question
        ^parameters <params>
        ^satisfaction <sat>)
   (<sat> ^action-event point)
   (<params> ^description <desc>)
}

# Returns points to the described object

sp {which-question*propose*find-object
   (state <s> ^topstate <s>
             -^instructor-message <msg>
              ^interaction.stack.segment.purpose <p>)
   (<p> ^type which-question)
-->
   (<s> ^operator <o> +)
   (<o> ^name find-object
        ^return-any true
        ^category object-processing)
}

sp {which-question*find-object*elaborate*word
   (state <s> ^operator <o> +
              ^interaction.stack.segment.purpose <p>)
   (<p> ^type which-question
        ^parameters.description.word {<word> <> object })
   (<o> ^name find-object)
-->
   (<o> ^word <word>)
}

sp {which-question*apply*find-object
   (state <s> ^operator <o>
              ^interaction.stack.segment.purpose.type which-question
              ^status <status>)
   (<o> ^name find-object
        ^match <m>)
-->
   (<status> ^performed.action-event point)
}

sp {recogition-question*apply*failure*send-message
   (state <s> ^operator <o>
              ^io.output-link <out>
              ^interaction.stack.segment.purpose.type which-question)
   (<o> ^name find-object
        ^match failure)
-->
   (<out> ^send-message <msg>)
   (<msg> ^type no-object
          ^fields <f>)
}

sp {recogition-question*apply*find-object*point
   (state <s> ^operator <o>
              ^io.output-link <out>
              ^topstate.object <obj>
              ^interaction.stack.segment.purpose.type which-question)
   (<o> ^name find-object
        ^match.id <id>)
   (<obj> ^id <id>
          ^pose <pose>)
-->
   (<out> ^point <point>)
   (<point> ^id <id>
            ^pose <pose>)
}