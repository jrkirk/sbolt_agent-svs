# Fetch the label for the category-word pair from memory
sp {this-object-message*propose*lookup-smem*label
   (state <s> ^name this-object-message
              ^nounjective <nj>)
   (<nj> ^category {<cat> <> failure <> generic}
          ^word <word>
         -^label)
-->
   (<s> ^operator <o> + =)
   (<o> ^name lookup-smem
        ^category smem
        ^lookup-type label
        ^query <q>
        ^desired.label true)
   (<q> ^type label-map ^word <word> ^category <cat> ^label <label>)
}

# Successful retrieval
sp {this-object-message*apply*lookup-smem*label*success
   (state <s> ^name this-object-message
              ^operator <o>
              ^nounjective <nj>)
   (<o> ^name lookup-smem
        ^lookup-type label
        ^query.word <word>
        ^result {<label> <> failure})
   (<nj> ^word <word>)
-->
   (<nj> ^label <label>)
}

# Send a training label to the visual system
sp {this-object-message*apply*lookup-smem*label*send-training-label
   (state <s> ^name this-object-message
              ^io.output-link <out>
              ^operator <o>
              ^this-object.id <id>)
   (<o> ^name lookup-smem
        ^lookup-type label
        ^query.category <cat>
        ^result {<label> <> failure})
-->
   (<out> ^send-training-label <stl>)
   (<stl> ^id <id> ^label <label> ^category <cat>)
}

sp {this-object-message*apply*lookup-smem*label*annotate*name
   (state <s> ^name this-object-message
              ^this-object.property <prop>
              ^operator <o>)
   (<o> ^name lookup-smem
        ^lookup-type label
        ^query.category name
        ^query.word <word>
        ^result <> failure)
   (<prop> ^name name)
-->
   (<prop> ^word <word>)
}