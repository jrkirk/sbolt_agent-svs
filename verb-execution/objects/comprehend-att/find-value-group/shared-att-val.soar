# The currently examined att-val pairing is the only unknown att-val pair
# the objects all have in common
sp {find-value-group*propose*shared-att-val
   (state <s> ^name find-value-group
              ^att-type <att-type>
              ^att-to-comp <att-to-comp>
             -^compared <att-to-comp>)
   -{(<s> ^common-av-pair <av-pair>)
   	 (<av-pair> ^known false
                ^att-type <att-type>
                ^att {<att> <> <att-to-comp>})}
-->
   (<s> ^operator <o> + =)
   (<o> ^name shared-att-val
        ^category object-processing)
}

# If the attribute is internal, then create a query structure
sp {find-value-group*elaborate*query
   (state <s> ^name find-value-group
              ^att-type internal
              ^operator <o> +
              ^att-to-comp <att-to-comp>)
   (<o> ^name shared-att-val)
-->
   (<o> ^query <q>)
   (<q> ^type shared-att-val
   		^internal-att <att-to-comp>)
}

# If the attribute is internal, add group-obj to the query structure
sp {find-value-group*elaborate*shared-att-val*group-obj
   (state <s> ^name find-value-group
              ^att-type internal
              ^operator <o> +
              ^object <obj>)
   (<o> ^name shared-att-val
        ^query <q>)
-->
   (<q> ^group-obj <obj>)
}

# If the attribute is internal, add exceptions to the query structure
sp {find-value-group*elaborate*shared-att-val*exception
   (state <s> ^name find-value-group
              ^att-type internal
              ^operator <o> +
              ^att-to-comp <att-to-comp>
              ^common-av-pair <common-av-pair>
              ^object.mapping.att-val-pair <av-pair>)
   (<o> ^name shared-att-val
        ^query <q>)
   (<common-av-pair> ^known true
                     ^att-type internal
                     ^att {<att> <> <att-to-comp>})
   (<av-pair> ^att.internal <att>
              ^att.external <external-att>)
-->
   (<q> ^exception <external-att>)
}

# For external attributes, this identifies the potential matches to internal ones
# Potential match if there is an internal att-val pair shared by the objects
sp {find-value-group*elaborate*shared-att-val*potential*match
   (state <s> ^name find-value-group
              ^att-type external
              ^operator <o> +
              ^common-av-pair <common-av-pair>)
   (<o> ^name shared-att-val)
   (<common-av-pair> ^known false
                     ^att-type internal
                     ^att <internal-att>)
-->
   (<o> ^potential-match <internal-att>)
}


# Mark compared
sp {find-value-group*apply*shared-att-val*mark-compared
   (state <s> ^name find-value-group
              ^operator <o>
              ^att-to-comp <att>)
   (<o> ^name shared-att-val)
-->
   (<s> ^compared <att>)
}

# For internal attributes, put the query on the top state
sp {find-value-group*apply*shared-att-val*create-query
   (state <s> ^name find-value-group
              ^att-type internal
              ^operator <o>
              ^topstate.queries <qs>)
   (<o> ^name shared-att-val
        ^query <q>)
-->
   (<qs> ^query <q>)
}

# If there is also only 1 unknown internal att-val pair held in common, 
# Do a match and learn that attribute
sp {find-value-group*apply*shared-att-val*external*learn-att
   (state <s> ^name find-value-group
              ^operator <o>
              ^att-type external
              ^att-to-comp <external-att>
              ^topstate.status <status>)
   (<o> ^name shared-att-val
        ^potential-match <internal-att>
       -^potential-match <> <internal-att>)
-->
   (<status> ^type learning
             ^state-name topstate
             ^purpose <p>)
   (<p> ^type learn-att
        ^parameters <params>
        ^satisfaction <sat>)
   (<params> ^internal <internal-att>
             ^external <external-att>)
   (<sat> ^learning-event attribute)
}