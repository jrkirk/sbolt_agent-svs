# propose*comprehend-att


# Proposed for a ^comprehend-att.<att-type> <attribute> structure on the top state
# Removes it when finished

# If there is a run-comprehend-att flag on the topstate, propose comprehend-att
sp {propose*comprehend-att
   (state <s> ^topstate <s>
              ^comprehend-att.<att-type> <attribute>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name comprehend-att
        ^category object-processing
        ^attribute <attribute>
        ^type <att-type>)
}

# Adds all the uniquely-named objects that have that attribute to the operator
sp {elaborate*comprehend-att*objects
   (state <s> ^operator <o> +
              ^topstate.object <obj>)
   (<o> ^name comprehend-att
        ^attribute <att>
        ^type <att-type>)
   (<obj> ^mapping.att-val-pair.att.<att-type> <att>
          ^unique true)
-->
   (<o> ^object <obj>)
}

# Rejects the operator if there are no objects with that attribute
sp {reject*if*no*objects
   (state <s> ^operator <o> +)
   (<o> ^name comprehend-att
       -^object <obj>)
-->
   (<s> ^operator <o> -)
}

# Prefers external attribute processing (which doesn't require the user's input) to internal
sp {comprehend-att*prefer*external
   (state <s> ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name comprehend-att
         ^type external)
   (<o2> ^name comprehend-att
         ^type internal)
-->
   (<s> ^operator <o1> > <o2>)
}