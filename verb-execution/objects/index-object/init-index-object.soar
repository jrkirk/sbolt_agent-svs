# init-index-object
# This operator sets up the index-object substate including:
#	Restoring an existing index-object-context or creating a new one

sp {index-object*propose*init-index-object
   (state <s> ^name index-object
             -^index-object-context)
-->
   (<s> ^operator <o> +)
   (<o> ^name init-index-object
        ^category indexing)
}


# index-object-context: If there is an existing one on the topstate then copy it
sp {index-object*apply*init-index-object*existing*context
   (state <s> ^name index-object
              ^operator <o>
              ^topstate <top>)
   (<o> ^name init-index-object)
   (<top> ^interaction.stack.segment <seg>
          ^index-object-context <context>)
   (<context> ^segment <seg>)
-->
   (<s> ^index-object-context <context>)
}

# If for some reason the segment associated with an index-object-context is popped, remove the context
sp {pop-segment*remove*index-object-context
   (state <s> ^superstate <s>
              ^operator.name pop-segment
              ^interaction.stack.segment <seg>
              ^index-object-context <context>)
   (<context> ^segment <seg>)
-->
   (<s> ^index-object-context <context> -)
}

#--------- Create new context ----------#

sp {index-object*apply*init-index-object*new*context
   (state <s> ^name index-object
              ^operator.name init-index-object
              ^topstate <top>)
   (<top> ^interaction.stack.segment <seg>
          -^index-object-context.segment <seg>)
-->
   (<s> ^copy-context <cc>)
   (<cc> ^context.segment <seg>)
}

#----------- Copy Parameters ----------------#

sp {index-object*apply*init-index-object*mark*parameters*copied
   (state <s> ^name index-object
              ^operator.name init-index-object
              ^copy-context <cc>)
   (<cc> -^copied-parameters)
-->
   (<cc> ^copied-parameters true)
}

sp {index-object*apply*init-index-object*copy*parameters
   (state <s> ^name index-object
              ^operator.name init-index-object
              ^copy-context <cc>)
   (<cc> ^context <context>
        -^copied-parameters)
   (<s> ^{<arg> << use-interaction allow-ties return-all >>} <val>)
-->
   (<context> ^<arg> <val>)
}

#------- Copy Objects/Relations Depth1 ----------#

sp {index-object*apply*init-index-object*mark*depth1*copied
   (state <s> ^name index-object
              ^operator.name init-index-object
              ^copy-context <cc>)
   (<cc> -^copied-depth1)
-->
   (<cc> ^copied-depth1 true)
}

sp {index-object*apply*init-index-object*copy*objects*relations*depth1
   (state <s> ^name index-object
              ^operator.name init-index-object
              ^superstate.operator <o>
              ^copy-context <cc>)
   (<cc> ^context <context>
        -^copied-depth1)
   (<o> ^{<type> << object relation >>} <val>)
-->
   (<context> ^<type> <val-copy>)
   (<cc> ^map <map>)
   (<map> ^original <val>
          ^copy <val-copy>)
}

#------- Copy Objects/Relations Depth2 ----------#

sp {index-object*apply*init-index-object*mark*depth2*copied
   (state <s> ^name index-object
              ^operator.name init-index-object
              ^copy-context <cc>)
   (<cc> ^copied-depth1 true
        -^copied-depth2)
-->
   (<cc> ^copied-depth2 true)
}

sp {index-object*apply*init-index-object*copy*words*depth2
   (state <s> ^name index-object
              ^operator.name init-index-object
              ^copy-context <cc>)
   (<cc> ^context <context>
         ^copied-depth1 true
        -^copied-depth2
         ^map <map>)
   (<map> ^original <val>
          ^copy <val-copy>)
   (<val> ^word {<word> <> UNKNOWN})
-->
   (<val-copy> ^property.word <word>)
}

sp {index-object*apply*init-index-object*copy*objects*relations*depth2*resolve*references
   (state <s> ^name index-object
              ^operator.name init-index-object
              ^copy-context <cc>)
   (<cc> ^context <context>
         ^copied-depth1 true
        -^copied-depth2
         ^map <map>
         ^map <map2>)
   (<map> ^original <val>
          ^copy <val-copy>)
   (<val> ^<arg> <arg-val>)
   (<map2> ^original <arg-val>
           ^copy <arg-val-copy>)
-->
   (<val-copy> ^<arg> <arg-val-copy>)
}
              
#------ Finished -----#
sp {index-object*apply*init-index-object*copy*created*context
   (state <s> ^name index-object
              ^operator.name init-index-object
              ^copy-context <cc>
              ^topstate <top>)
   (<cc> ^copied-depth2 true
         ^context <context>)
-->
   (<s> ^index-object-context <context>)
   (<top> ^index-object-context <context>)
}
              