# If the state of an object changes, this updates the object representation to reflect it

# The state value on the input-link doesn't match the one on the object
sp {propose*change-state-property
   (state <s> ^topstate <s>
              ^io.input-link.objects.object <io-obj>
              ^object <obj>)
   (<io-obj> ^id <id>
             ^state-prop <io-prop>)
   (<io-prop> ^name <prop-name>
              ^type state
              ^value <prop-val>)
   (<obj> ^id <id>
          ^property <prop>)
   (<prop> ^name <prop-name>
          -^value <prop-val>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name change-state-property
        ^category object-processing
        ^property <prop>
        ^new-value <prop-val>)
}

# Change the value structure on the property
sp {apply*change-state-property
   (state <s> ^operator <o>)
   (<o> ^name change-state-property
        ^property <prop>
        ^new-value <new-value>)
   (<prop> ^value <old-value>)
-->
   (<prop> ^value <old-value> -
           ^value <new-value>)
}