epmem --set learning on
epmem --set trigger dc

sp {lookup-object*propose*query-epmem-id
   (state <s> ^name lookup-object
             -^queried-id)
-->
   (<s> ^operator <o> +)
   (<o> ^name query-epmem-id
        ^category object-processing)
}

sp {lookup-object*apply*query-epmem-id
   (state <s> ^name lookup-object
              ^operator.name query-epmem-id
              ^object.id <id>
              ^epmem.command <cmd>)
-->
   (<s> ^queried-id true)
   (<cmd> ^query <q>)
   (<q> ^superstate nil
        ^object.id <id>)
}

sp {lookup-object*propose*epmem-result-id
   (state <s> ^name lookup-object
              ^queried-id
             -^retrieved-id
              ^epmem.result.retrieved <ret>)
-->
   (<s> ^operator <o> +)
   (<o> ^name epmem-result-id
        ^category object-processing
        ^retrieved <ret>)
}

sp {lookup-object*apply*epmem-result-id
   (state <s> ^name lookup-object
              ^operator.name epmem-result-id)
-->
   (<s> ^retrieved-id true)
}

sp {lookup-object*apply*epmem-result-id*success
   (state <s> ^name lookup-object
              ^operator <o>
              ^object.id <id>)
   (<o> ^name epmem-result-id
        ^retrieved.object <obj>)
   (<obj> ^id <id>)
-->
   (<s> ^match <obj>)
}



