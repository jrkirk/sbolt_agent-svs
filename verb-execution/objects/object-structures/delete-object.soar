# When an object no longer exists on the input-link remove it

sp {apply*any-op*set*delete-time
   (state <s> ^topstate <s>
              ^operator <o>
              ^object <obj>
             -^io.input-link.objects.object.id <id>
              ^io.input-link.time.seconds <sec>)
   (<obj> ^id <id>
         -^held
          -^delete-time)
-->
   (<obj> ^delete-time (+ <sec> 2))
}

sp {apply*any-op*remove*delete-time
   (state <s> ^topstate <s>
              ^operator <o>
              ^object <obj>
              ^io.input-link.objects.object.id <id>)
   (<obj> ^id <id>
          ^delete-time <time>)
-->
   (<obj> ^delete-time <time> -)
}

sp {propose*delete-object
   (state <s> ^topstate <s>
              ^object <obj>
              ^io.input-link <in>)
   (<in> -^objects.object.id <id>
          ^self.action << wait failure >>
          ^time.seconds <cur-time>)
   (<obj> ^id <id>
          ^delete-time < <cur-time>
         -^held)
-->
   (<s> ^operator <o> + =)
   (<o> ^name delete-object
        ^category object-processing
        ^object <obj>)
}

sp {apply*delete-object
   (state <s> ^operator <o>)
   (<o> ^name delete-object 
        ^object <obj>)
-->
   (<s> ^object <obj> -)
}