# When there is a new object on the input link initialize a new object sturcture in working memory
sp {propose*create-object
   (state <s> ^topstate <s>
              ^io.input-link.objects.object.id <id>
             -^object.id <id>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name create-object
        ^category object-processing
        ^id <id>)
}

# Elaborates an object sturcture other operators can add to 
sp {elaborate*create-object*object
	(state <s> ^operator <o> +)
   (<o> ^name create-object
        ^id <id>)
-->
   (<o> ^object <obj>)
}

# Adds the object structure to the topstate
sp {apply*create-object
   (state <s> ^operator <o>)
   (<o> ^name create-object
        ^object <obj>)
-->
   (<s> ^object <obj>)
}

# Adds the name to the object
sp {apply*create-object*name
   (state <s> ^operator <o>)
   (<o> ^name create-object
        ^object <obj>)
-->
   (<obj> ^property <prop>)
   (<prop> ^name name
           ^type descriptive)
}

# Copies the id of the object
sp {apply*create-object*id
   (state <s> ^operator <o>)
   (<o> ^name create-object
        ^object <obj>
        ^id <id>)
-->
   (<obj> ^id <id>)
}