# Used to get the word for a given label
# Expected Operator:
#   (<s> ^operator <o> +)
#   (<o> ^name lookup-smem
#        ^lookup-type word
#        ^query <q>
#        ^desired.word true)
#   (<q> ^type label-map ^word w6 ^category c34 ^label <label>)
# only the label is actually specified

# If the word is already known then skip an smem lookup
sp {apply*lookup-smem*word*already*known
   (state <s> ^topstate.object.property.value <val>
              ^operator <o>)
   (<o> ^name lookup-smem
        ^lookup-type word
        ^query.label <label>)
   (<val> ^label <label>
          ^word <word>)
-->
   (<o> ^result <word>)
}

# Updates any objects with the word, for convenience
sp {apply*lookup-smem*word*annotate*object
   (state <s> ^topstate.object.property.value <val>
              ^operator <o>)
   (<o> ^name lookup-smem
        ^lookup-type word
        ^query.label <label>
        ^result {<word> <> failure})
   (<val> ^label <label>
         -^word <word>)
-->
   (<val> ^word <word>)
}