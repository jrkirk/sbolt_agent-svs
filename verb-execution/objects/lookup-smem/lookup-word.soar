# Used to get the word for a given label
# Expected Operator:
#   (<s> ^operator <o> +)
#   (<o> ^name lookup-smem
#        ^lookup-type word
#        ^query <q>
#        ^desired.word true)
#   (<q> ^type label-map ^word w6 ^category c34 ^label <label>)
# only the label is actually specified

# The label is cached in working memory, use it
sp {lookup-smem*word*working-mem
   (state <s> ^operator <o>
              ^topstate.mappings.label-map <lm>)
   (<o> ^name lookup-smem
        ^lookup-type word
        ^query.label <label>)
   (<lm> ^<word>.<cat> <label>)
-->
   (<o> ^result <word>)
}

# Updates any objects with the word, for convenience
sp {apply*lookup-smem*word*annotate*object
   (state <s> ^topstate.object.property.value <val>
              ^operator <o>)
   (<o> ^name lookup-smem
        ^lookup-type word
        ^query.label <label>
        ^result {<word> <> failure})
   (<val> ^label <label>
         -^word <word>)
-->
   (<val> ^word <word>)
}