sp {lookup-smem*propose*lookup
   (state <s> ^name lookup-smem
              ^retrieval-count <c>)
-->
   (<s> ^operator <o> +)
   (<o> ^name lookup
        ^category smem)
}


sp {lookup-smem*apply*lookup
   (state <s> ^name lookup-smem
              ^operator.name lookup
              ^query <q>
              ^smem.command <cmd>)
-->
   (<cmd> ^query <q>)
}

sp {lookup-smem*elaborate*lookup*add*prohibit
   (state <s> ^name lookup-smem
              ^operator.name lookup
              ^smem.command <cmd>
              ^retrieved <ret>)
   (<cmd> ^query <q>)
-->
   (<cmd> ^prohibit <ret>)
}
              

# Remove the query from the smem.command link
sp {lookup-smem*apply*lookup*remove*query
   (state <s> ^name lookup-smem
              ^operator.name lookup
              ^smem <smem>)
   (<smem> ^command <cmd>
           ^result.{<< success failure >>} <query>)
   (<cmd> ^query <query>)
-->
   (<cmd> ^query <query> -)
}

# If it failed on the first retrieval copy a failure
sp {lookup-smem*apply*lookup*copy*failure
   (state <s> ^name lookup-smem
              ^operator.name lookup
              ^smem.result.failure
              ^retrieval-count 0)
-->
   (<s> ^result failure)
}

sp {lookup-smem*apply*lookup*failure*finished
   (state <s> ^name lookup-smem
              ^operator.name lookup
              ^smem.result.failure)
-->
   (<s> ^finished true)
}

# Case 2:  success
sp {lookup-smem*apply*lookup*success*copy-result
   (state <s> ^name lookup-smem
              ^operator.name lookup
              ^smem.result <smem-result>
              ^desired <d>
              ^superstate.operator <ss-op>)
   (<d> ^<att> <v>)
   (<smem-result> ^success
                  ^retrieved <ret>)
   (<ret> ^<att> <result>)
-->
   (<s> ^result <result>
        ^retrieved <ret>)
}

# Increment the retrieval counter if success
sp {lookup-smem*apply*lookup*success*increment*counter
   (state <s> ^name lookup-smem
              ^operator.name lookup
              ^smem.result.{<< success failure >>}
              ^retrieval-count <c>)
-->
   (<s> ^retrieval-count <c> -
        ^retrieval-count (+ <c> 1))
}