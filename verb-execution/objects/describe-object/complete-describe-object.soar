sp {describe-object*propose*complete-describe-object
   (state <s> ^name describe-object)
-->
   (<s> ^operator <o> +)
   (<o> ^name complete-describe-object
        ^category object-processing)
}

sp {describe-object*apply*complete-describe-object*description
   (state <s> ^name describe-object
              ^operator.name complete-describe-object
             -^description)
-->
   (<s> ^description <desc>)
}

sp {describe-object*apply*complete-describe-object*name
   (state <s> ^name describe-object
              ^operator.name complete-describe-object
              ^description <desc>
              ^needs-found <nf>)
   (<nf> ^query name
         ^result <name>)
-->
   (<desc> ^word <name>)
}

sp {describe-object*apply*complete-describe-object*label
   (state <s> ^name describe-object
              ^operator.name complete-describe-object
              ^description <desc>
              ^needs-found <nf>
              ^needs-found <name-nf>)
   (<nf> ^query <> name
         ^result {<word> <> failure <> <name>})
   (<name-nf> ^query name
              ^result <name>)
-->
   (<desc> ^adjective <word>)
}

sp {describe-object*apply*complete-describe-object*finished
   (state <s> ^name describe-object
              ^operator.name complete-describe-object
              ^description <desc>)
-->
   (<s> ^finished true)
}

sp {describe-object*apply*complete-describe-object
   (state <s> ^name describe-object
              ^operator.name complete-describe-object
              ^description <desc>
              ^finished true
              ^superstate.operator <ss-o>)
-->
   (<ss-o> ^description <desc>)
}

sp {describe-object*apply*complete-describe-object*failure
   (state <s> ^name describe-object
              ^operator.name complete-describe-object
             -^desc-object
              ^superstate.operator <ss-o>)
-->
   (<ss-o> ^description failure)
}

