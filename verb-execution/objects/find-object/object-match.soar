sp {find-object*propose*object-match
   (state <s> ^name find-object
              ^topstate.object <obj>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name object-match
        ^category object-processing
        ^object <obj>)
}

sp {find-object*reject*not-match*category
   (state <s> ^name find-object
              ^operator <o> +
              ^nounjective.category <cat-name>)
   (<o> ^name object-match
        ^object <obj>)
   (<obj> -^property.name <cat-name>)
-->
   (<s> ^operator <o> -
   	^not-match <obj>)
}

sp {find-object*reject*not-match*visual-label
   (state <s> ^name find-object
              ^operator <o> +
              ^nounjective <nj>)
   (<o> ^name object-match
        ^object <obj>)
   (<obj> ^property <prop>)
   (<prop> ^name <cat-name>
           ^type visual
           -^label <label>)
   (<nj> ^category <cat-name>
         ^label <label>)
-->
   (<s> ^operator <o> -
   	^not-match <obj>)
}

sp {find-object*reject*not-match*name
   (state <s> ^name find-object
              ^operator <o> +
              ^nounjective <nj>)
   (<o> ^name object-match
        ^object <obj>)
   (<obj> ^property <prop>)
   (<prop> ^name name
           -^word <name>)
   (<nj> ^category name
         ^word {<name> <> object})
-->
   (<s> ^operator <o> -
   	^not-match <obj>)
}

#### SM: changed to support multiple object matches, resolution is attempted later based on object affordances.

#sp {find-object*apply*object-match*indifference
#   (state <s> ^name find-object
#              ^operator <o> +
#	      -^not-match <obj>)
#   (<o> ^name object-match
#        ^object <obj>)
#-->
#   (<s> ^operator <o> =)
#}

sp {find-object*apply*object-match
   (state <s> ^name find-object
              ^operator <o>
             -^return-all true
              ^superstate.operator <ss-o>)
   (<o> ^name object-match
        ^object <obj>)
-->
   (<ss-o> ^match <obj>)
}

sp {find-object*apply*object-match*return-all
   (state <s> ^name find-object
              ^operator.name object-match
              ^operator <o1> +
              ^return-all true
              ^superstate.operator <ss-o>
             -^not-match <obj>)
   (<o1> ^name object-match
         ^object <obj>)
-->
   (<ss-o> ^match <obj>)
}


sp {find-object*add*smem-match
   (state <s> ^name find-object
              ^smem-match {<obj> <> failure}
              ^superstate.operator <ss-o>)
-->
   (<ss-o> ^match <obj>)
}

sp {find-object*propose*no-match
   (state <s> ^name find-object)
   	   #   ^smem-match failure) 
-->
   (<s> ^operator <o> +)
   (<o> ^name no-match
        ^category object-processing)
}

sp {find-object*apply*no-match
   (state <s> ^name find-object
              ^superstate.operator <o>
              ^operator.name no-match)
-->
   (<o> ^match failure)
}