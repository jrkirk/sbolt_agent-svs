sp {count-objects*propose*index-object
   (state <s> ^name count-objects
             -^match)
-->
   (<s> ^operator <o> +)
   (<o> ^name index-object
        ^return-all true
        ^use-interaction false
        ^allow-ties false
        ^infer-match false
        ^category object-processing)
}

sp {count-objects*elaborate*index-object*object
   (state <s> ^name count-objects
              ^operator <o> +
              ^superstate.operator.object <obj>)
   (<o> ^name index-object)
-->
   (<o> ^object <obj>)
}

sp {count-objects*elaborate*index-object*relation
   (state <s> ^name count-objects
              ^operator <o> +
              ^superstate.operator.relation <rel>)
   (<o> ^name index-object)
-->
   (<o> ^relation <rel>)
}


sp {count-objects*apply*index-object*copy-matches
   (state <s> ^name count-objects
              ^operator <o>)
   (<o> ^name index-object
        ^match <match>)
-->
   (<s> ^match <match>)
}

sp {count-objects*apply*index-object*count
   (state <s> ^name count-objects
              ^operator.name index-object)
-->
   (<s> ^match-count 0)
}